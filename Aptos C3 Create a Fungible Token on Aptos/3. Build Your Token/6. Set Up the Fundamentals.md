# Set Up the Fundamentals

In this lesson, we will walk through the process of building three fundamental functions—mint, transfer, and burn—that are essential for any token contract. These functions will be implemented in a smart contract to manage a fungible asset. 

## Mint Function

![1 (2).gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/assets-for-aptos-c3/Section%203%20Build%20Your%20Token/Lesson%206%20Set%20Up%20the%20Fundamentals/1_(2).webp?raw=true)

The mint function creates new tokens and transfers them to a specified address. It is typically used to mint new tokens into the circulating supply.

```
 public entry fun mint(admin: &signer, to: address, amount: u64) acquires ManagedFungibleAsset {
        let asset = get_metadata();
        let managed_fungible_asset = authorized_borrow_refs(admin, asset);
        let to_wallet = primary_fungible_store::ensure_primary_store_exists(to, asset);
        let fa = fungible_asset::mint(&managed_fungible_asset.mint_ref, amount);
        fungible_asset::deposit_with_ref(&managed_fungible_asset.transfer_ref, to_wallet, fa);
    }
```

### Code Explanation

Now, let’s try to understand this function:

```
public entry fun mint(admin: &signer, to: address, amount: u64) acquires ManagedFungibleAsset
```

- Public entry makes sure that this function is public and can be called externally. It requires a signer (`admin`), a recipient address (`to`), and an amount to be minted (`amount`). It also acquires the `ManagedFungibleAsset` resource which basically stores the ability to mint, transfer and burn if you remember from our last lesson.

```jsx
let asset = get_metadata();
```

- First, let's fetch the metadata object associated with the fungible asset

```jsx
let managed_fungible_asset = authorized_borrow_refs(admin, asset);
```

- Ensure the caller is authorized to perform the mint operation.

```jsx
let to_wallet = primary_fungible_store::ensure_primary_store_exists(to, asset);
```

- Now let's ensure that the recipient's wallet exists. If not, it creates one.

```jsx
let fa = fungible_asset::mint(&managed_fungible_asset.mint_ref, amount);
```

- Now let's mint the specified amount of tokens.

```jsx
fungible_asset::deposit_with_ref(&managed_fungible_asset.transfer_ref, to_wallet, fa);
```

- And finally, deposit the newly minted tokens into the recipient's wallet.

## Transfer Function

![2 (1).gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/assets-for-aptos-c3/Section%203%20Build%20Your%20Token/Lesson%206%20Set%20Up%20the%20Fundamentals/2_(1).webp?raw=true)

The transfer function moves tokens from one address to another. It is used to facilitate transactions between users.

```jsx
public entry fun transfer(admin: &signer, from: address, to: address, amount: u64) acquires ManagedFungibleAsset {
        let asset = get_metadata();
        let transfer_ref = &authorized_borrow_refs(admin, asset).transfer_ref;
        let from_wallet = primary_fungible_store::primary_store(from, asset);
        let to_wallet = primary_fungible_store::ensure_primary_store_exists(to, asset);
        fungible_asset::transfer_with_ref(transfer_ref, from_wallet, to_wallet, amount);
    }
```

### Code Explanation

Now, let’s try to understand this function:

```jsx
public entry fun transfer(admin: &signer, from: address, to: address, amount: u64) acquires ManagedFungibleAsset
```

- Again this function is public and can be called externally. It requires a signer (`admin`), the sender address (`from`), the recipient address (`to`), and the amount to transfer (`amount`). It also acquires the `ManagedFungibleAsset` resource.

```jsx
let asset = get_metadata();
```

- So we will start with fetching the metadata object associated with the fungible asset.

```jsx
let transfer_ref = &authorized_borrow_refs(admin, asset).transfer_ref;
```

- This line of code ensures the caller is authorized to perform the transfer operation.

```jsx
let from_wallet = primary_fungible_store::primary_store(from, asset);
```

- Now let's retrieve the sender's wallet.

```jsx
let to_wallet = primary_fungible_store::ensure_primary_store_exists(to, asset);
```

- Ensures the recipient's wallet exists. If not, this line creates one.

```jsx
fungible_asset::transfer_with_ref(transfer_ref, from_wallet, to_wallet, amount);
```

- Transfers the specified amount of tokens from the sender's wallet to the recipient's wallet.

## Burn Function

![3.gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/assets-for-aptos-c3/Section%203%20Build%20Your%20Token/Lesson%206%20Set%20Up%20the%20Fundamentals/3.webp?raw=true)

The burn function destroys a specified amount of tokens from a given address, reducing the circulating supply.

```jsx
 public entry fun burn(admin: &signer, from: address, amount: u64) acquires ManagedFungibleAsset {
        let asset = get_metadata();
        let burn_ref = &authorized_borrow_refs(admin, asset).burn_ref;
        let from_wallet = primary_fungible_store::primary_store(from, asset);
        fungible_asset::burn_from(burn_ref, from_wallet, amount);
    }
```

### Code Explanation

Now, let’s try to understand this function:

```jsx
public entry fun burn(admin: &signer, from: address, amount: u64) acquires ManagedFungibleAsset
```

- The function is public and can be called externally. It requires a signer (`admin`), the address from which to burn tokens (`from`), and the amount to burn (`amount`). It also acquires the `ManagedFungibleAsset` resource.

```jsx
let asset = get_metadata();
```

- Fetches the metadata object associated with the fungible asset.

```jsx
let burn_ref = &authorized_borrow_refs(admin, asset).burn_ref;
```

- Ensures the caller is authorized to perform the burn operation.

```jsx
let from_wallet = primary_fungible_store::primary_store(from, asset);
```

- Retrieves the wallet of the address from which tokens are to be burned.

```jsx
fungible_asset::burn_from(burn_ref, from_wallet, amount);
```

- Burns the specified amount of tokens from the given address's wallet.

## Complete Code

Here is the complete code for the token contract, so add the following code under `module aptos_asset::fungible_asset` after the `get_metadata()` function: 

```
public entry fun mint(admin: &signer, to: address, amount: u64) acquires ManagedFungibleAsset {
        let asset = get_metadata();
        let managed_fungible_asset = authorized_borrow_refs(admin, asset);
        let to_wallet = primary_fungible_store::ensure_primary_store_exists(to, asset);
        let fa = fungible_asset::mint(&managed_fungible_asset.mint_ref, amount);
        fungible_asset::deposit_with_ref(&managed_fungible_asset.transfer_ref, to_wallet, fa);
    }// <:!:mint

    /// Transfer as the owner of metadata object ignoring `frozen` field.
    public entry fun transfer(admin: &signer, from: address, to: address, amount: u64) acquires ManagedFungibleAsset {
        let asset = get_metadata();
        let transfer_ref = &authorized_borrow_refs(admin, asset).transfer_ref;
        let from_wallet = primary_fungible_store::primary_store(from, asset);
        let to_wallet = primary_fungible_store::ensure_primary_store_exists(to, asset);
        fungible_asset::transfer_with_ref(transfer_ref, from_wallet, to_wallet, amount);
    }

    /// Burn fungible assets as the owner of metadata object.
    public entry fun burn(admin: &signer, from: address, amount: u64) acquires ManagedFungibleAsset {
        let asset = get_metadata();
        let burn_ref = &authorized_borrow_refs(admin, asset).burn_ref;
        let from_wallet = primary_fungible_store::primary_store(from, asset);
        fungible_asset::burn_from(burn_ref, from_wallet, amount);
    }
```

Make sure your `create-fungible-token-on-aptos/sources/fungible_asset.move` file is updated with the latest code and then in your opened terminal, run the following commands:

```
git add .
git commit -m "code update"
git push
```

## That’s a Wrap

That’s all for this lesson. Congratulations!!! We have so much more to explore which we will do in the next one. By implementing these core functions, we empower users to interact with our token contract and manage the supply and distribution of our fungible tokens on the Aptos blockchain.

![Frame 3560414.gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/assets-for-aptos-c3/Section%203%20Build%20Your%20Token/Lesson%206%20Set%20Up%20the%20Fundamentals/Frame_3560414.webp?raw=true)