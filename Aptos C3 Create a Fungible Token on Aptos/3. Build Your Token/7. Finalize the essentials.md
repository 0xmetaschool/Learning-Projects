# Finalize the essentials

Welcome back gang! Let’s add in some more functionalities to our token contract. We want our token to be able to be withdrawn and deposited seamlessly. But hey, it’s not just about moving tokens around; we also want to give accounts the option to freeze or unfreeze, adding an extra layer of control to our system. So let’s get these functions implemented. 
![aptos04-ezgif com-resize](https://github.com/user-attachments/assets/72b7cf0d-8cbc-47fd-84eb-b7a0d2e4a4a3)


## Authorized Borrow References

Before we head out into implementing all of these functions, It’s super important to ensure that only authorized individuals can perform sensitive operations. So we’ll create a function called `authorized_borrow_refs` to verify ownership and provide access to the necessary references for managing assets.

```
inline fun authorized_borrow_refs(
    owner: &signer,
    asset: Object<Metadata>,
): &ManagedFungibleAsset acquires ManagedFungibleAsset {
    assert!(object::is_owner(asset, signer::address_of(owner)), error::permission_denied(ENOT_OWNER));
    borrow_global<ManagedFungibleAsset>(object::object_address(&asset))
}
```

The `authorized_borrow_refs` function will take two parameters:

- `owner`: The signer (Owner/Administrator) who is requesting access to the asset references.
- `asset`: The metadata object associated with the token, containing essential information about the token.

In-order to perform the verification, we will use the `is_owner` function to check if the `owner` we have provided via the parameters is actually the owner or not using the `assert` macro. If the assertion fails we will throw a `permission_denied(ENOT_OWNER)` error.

Finally If ownership checks out, the function then grabs a managed reference of `ManagedFungibleAsset` to the global resource using `object_address(&asset)`. We have used the same function in our previous function definitions as well.

## Withdraw

The `withdraw` function will be responsible to let the token owner withdraw tokens from a specified address. It returns a `FungibleAsset` representing the withdrawn tokens and acquires the `ManagedFungibleAsset` resource.

```
public fun withdraw(admin: &signer, amount: u64, from: address): FungibleAsset acquires ManagedFungibleAsset {
    let asset = get_metadata();
    let transfer_ref = &authorized_borrow_refs(admin, asset).transfer_ref;
    let from_wallet = primary_fungible_store::primary_store(from, asset);
    fungible_asset::withdraw_with_ref(transfer_ref, from_wallet, amount)
}
```

The withdraw function will take three parameters :

- `admin`: The signer who is authorizing the withdrawal. This should be the owner of the metadata object.
- `amount`: The amount of tokens to withdraw.
- `from`: The address from which the tokens will be withdrawn.

We will be using the `withdraw_with_ref()` from the `fungible_asset` module we imported to perform the withdraw operation. This function requires a few parameters for it to function so let’s get them sorted.

- `get_metadata()` retrieves the metadata object associated with the token.
- `authorized_borrow_refs(admin, asset)` function we had created earlier verifies that the admin owns the metadata object and provides the  `transfer_ref to` authorize and execute token transfers.
- `primary_fungible_store::primary_store(from, asset)` retrieves the primary store (wallet) of the address `from` for the given `asset`. This store holds the tokens of the `from` address.

Finally we can use the above data with the `fungible_asset::withdraw_with_ref` function to withdraw the specified amount of tokens from `from_wallet` using `transfer_ref` to ensure authorization.

## Deposit

The `deposit` function allows the owner of the token to deposit a specified amount of tokens into a given address. 

```
public fun deposit(admin: &signer, to: address, fa: FungibleAsset) acquires ManagedFungibleAsset {
    let asset = get_metadata();
    let transfer_ref = &authorized_borrow_refs(admin, asset).transfer_ref;
    let to_wallet = primary_fungible_store::ensure_primary_store_exists(to, asset);
    fungible_asset::deposit_with_ref(transfer_ref, to_wallet, fa);
}
```

The `deposit` function will also take these parameters:

- `admin`: The signer authorizing the deposit. This should be the owner of the token.
- `to`: The address where the tokens will be deposited.
- `fa`: The `FungibleAsset` representing token to deposit.

We will be using the `deposit_with_ref()` from the `fungible_asset` module in order to perform the deposit operation. This function too requires a few parameters for it to function :

- `get_metadata()` retrieves the metadata object associated with the token and stores them as the `asset` variable.
- `authorized_borrow_refs(admin, asset)` function from earlier for verification.
- `primary_fungible_store::ensure_primary_store_exists(to, asset)`: Ensures that a primary store (wallet) exists for the address `to` for the given `asset`. If it does not exist, it creates one.

Finally, we can use the above data with the `deposit_with_ref()` function to deposit the specified amount of tokens into `to_wallet` using `transfer_ref` to ensure authorization.

## Freezing an Account

Let’s assume we've got a token contract running smoothly, but suddenly, we spot some suspicious activity on one of the accounts. Maybe there's been a security breach, or the account owner is up to no good. That's when freezing an account becomes absolutely necessary to prevent damage. 

![4.gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/assets-for-aptos-c3/Section%203%20Build%20Your%20Token/Lesson%207%20Finalize%20the%20essentials/4.webp?raw=true)

```
public entry fun freeze_account(admin: &signer, account: address) acquires ManagedFungibleAsset {
    let asset = get_metadata();
    let transfer_ref = &authorized_borrow_refs(admin, asset).transfer_ref;
    let wallet = primary_fungible_store::ensure_primary_store_exists(account, asset);
    fungible_asset::set_frozen_flag(transfer_ref, wallet, true);
}
```

The `freeze_account` function accepts the following parameters:

- `admin`: The signer who is authorized to freeze the account. Typically, this would be the contract owner or an administrator.
- `account`: The address of the account whose assets are to be frozen.

We will be using the `set_frozen_flag` in order to freeze an account, this is how the function will work: 

1. Retrieve the asset metadata using `get_metadata()` .
2. Verify the admin's ownership using `authorized_borrow_refs` to get the transfer reference.
3. Ensure the account's primary store (wallet) exists with `ensure_primary_store_exists`
4. Finally set the asset's frozen flag to true using `set_frozen_flag` .

TL;DR The `freeze_account` function allows administrators to prevent unauthorized asset transfers from specific accounts.

## Unfreezing an Account

Now let’s suppose our account was previously frozen due to a security concern or suspicious activity. Now, after a thorough investigation and resolution of the issues, it's time to restore the account's functionality. The `unfreeze_account` function is designed for this purpose, allowing authorized administrators to unfreeze an account and resume normal operations.

![5.gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/assets-for-aptos-c3/Section%203%20Build%20Your%20Token/Lesson%207%20Finalize%20the%20essentials/5.webp?raw=true)

```
public entry fun unfreeze_account(admin: &signer, account: address) acquires ManagedFungibleAsset {
    let asset = get_metadata();
    let transfer_ref = &authorized_borrow_refs(admin, asset).transfer_ref;
    let wallet = primary_fungible_store::ensure_primary_store_exists(account, asset);
    fungible_asset::set_frozen_flag(transfer_ref, wallet, false);
}
```

The `unfreeze_account` function is almost the same as the `freeze_account` function, the only difference being that we will set the frozen flag to `false` and the entire implementation is the same as that of `freeze_acount`. 

## Complete Code

Here is the complete code for the `fungible_asset.move` :

```

module aptos_asset::fungible_asset{
    use aptos_framework::fungible_asset::{Self, MintRef, TransferRef, BurnRef, Metadata, FungibleAsset};
    use aptos_framework::object::{Self, Object};
    use aptos_framework::primary_fungible_store;
    use std::error;
    use std::signer;
    use std::string::utf8;
    use std::option;

    /// Only fungible asset metadata owner can make changes.
    const ENOT_OWNER: u64 = 1;

    const ASSET_SYMBOL: vector<u8> = b"META";

    #[resource_group_member(group = aptos_framework::object::ObjectGroup)]
    /// Hold refs to control the minting, transfer and burning of fungible assets.
    struct ManagedFungibleAsset has key {
        mint_ref: MintRef,
        transfer_ref: TransferRef,
        burn_ref: BurnRef,
    }

    /// Initialize metadata object and store the refs.
    // :!:>initialize
    fun init_module(admin: &signer) {
        let constructor_ref = &object::create_named_object(admin, ASSET_SYMBOL);
        primary_fungible_store::create_primary_store_enabled_fungible_asset(
            constructor_ref,
            option::none(),
            utf8(b"META Coin"), /* name */
            utf8(ASSET_SYMBOL), /* symbol */
            8, /* decimals */
            utf8(b"https://drive.google.com/file/d/1vFm-kF6O3onxPgFJ_rVLh9YGFT_fFWM6/view?usp=sharing"), /* icon */
            utf8(b"http://metaschool.so"), /* project */
        );

        // Create mint/burn/transfer refs to allow creator to manage the fungible asset.
        let mint_ref = fungible_asset::generate_mint_ref(constructor_ref);
        let burn_ref = fungible_asset::generate_burn_ref(constructor_ref);
        let transfer_ref = fungible_asset::generate_transfer_ref(constructor_ref);
        let metadata_object_signer = object::generate_signer(constructor_ref);
        move_to(
            &metadata_object_signer,
            ManagedFungibleAsset { mint_ref, transfer_ref, burn_ref }
        )// <:!:initialize
    }

    #[view]
    /// Return the address of the managed fungible asset that's created when this module is deployed.
    public fun get_metadata(): Object<Metadata> {
        let asset_address = object::create_object_address(&@aptos_asset, ASSET_SYMBOL);
        object::address_to_object<Metadata>(asset_address)
    }

    // :!:>mint
    /// Mint as the owner of metadata object.
    public entry fun mint(admin: &signer, to: address, amount: u64) acquires ManagedFungibleAsset {
        let asset = get_metadata();
        let managed_fungible_asset = authorized_borrow_refs(admin, asset);
        let to_wallet = primary_fungible_store::ensure_primary_store_exists(to, asset);
        let fa = fungible_asset::mint(&managed_fungible_asset.mint_ref, amount);
        fungible_asset::deposit_with_ref(&managed_fungible_asset.transfer_ref, to_wallet, fa);
    }// <:!:mint

    /// Transfer as the owner of metadata object ignoring `frozen` field.
    public entry fun transfer(admin: &signer, from: address, to: address, amount: u64) acquires ManagedFungibleAsset {
        let asset = get_metadata();
        let transfer_ref = &authorized_borrow_refs(admin, asset).transfer_ref;
        let from_wallet = primary_fungible_store::primary_store(from, asset);
        let to_wallet = primary_fungible_store::ensure_primary_store_exists(to, asset);
        fungible_asset::transfer_with_ref(transfer_ref, from_wallet, to_wallet, amount);
    }

    /// Burn fungible assets as the owner of metadata object.
    public entry fun burn(admin: &signer, from: address, amount: u64) acquires ManagedFungibleAsset {
        let asset = get_metadata();
        let burn_ref = &authorized_borrow_refs(admin, asset).burn_ref;
        let from_wallet = primary_fungible_store::primary_store(from, asset);
        fungible_asset::burn_from(burn_ref, from_wallet, amount);
    }

    /// Freeze an account so it cannot transfer or receive fungible assets.
    public entry fun freeze_account(admin: &signer, account: address) acquires ManagedFungibleAsset {
        let asset = get_metadata();
        let transfer_ref = &authorized_borrow_refs(admin, asset).transfer_ref;
        let wallet = primary_fungible_store::ensure_primary_store_exists(account, asset);
        fungible_asset::set_frozen_flag(transfer_ref, wallet, true);
    }

    /// Unfreeze an account so it can transfer or receive fungible assets.
    public entry fun unfreeze_account(admin: &signer, account: address) acquires ManagedFungibleAsset {
        let asset = get_metadata();
        let transfer_ref = &authorized_borrow_refs(admin, asset).transfer_ref;
        let wallet = primary_fungible_store::ensure_primary_store_exists(account, asset);
        fungible_asset::set_frozen_flag(transfer_ref, wallet, false);
    }

    /// Withdraw as the owner of metadata object ignoring `frozen` field.
    public fun withdraw(admin: &signer, amount: u64, from: address): FungibleAsset acquires ManagedFungibleAsset {
        let asset = get_metadata();
        let transfer_ref = &authorized_borrow_refs(admin, asset).transfer_ref;
        let from_wallet = primary_fungible_store::primary_store(from, asset);
        fungible_asset::withdraw_with_ref(transfer_ref, from_wallet, amount)
    }

    /// Deposit as the owner of metadata object ignoring `frozen` field.
    public fun deposit(admin: &signer, to: address, fa: FungibleAsset) acquires ManagedFungibleAsset {
        let asset = get_metadata();
        let transfer_ref = &authorized_borrow_refs(admin, asset).transfer_ref;
        let to_wallet = primary_fungible_store::ensure_primary_store_exists(to, asset);
        fungible_asset::deposit_with_ref(transfer_ref, to_wallet, fa);
    }

    /// Borrow the immutable reference of the refs of `metadata`.
    /// This validates that the signer is the metadata object's owner.
    inline fun authorized_borrow_refs(
        owner: &signer,
        asset: Object<Metadata>,
    ): &ManagedFungibleAsset acquires ManagedFungibleAsset {
        assert!(object::is_owner(asset, signer::address_of(owner)), error::permission_denied(ENOT_OWNER));
        borrow_global<ManagedFungibleAsset>(object::object_address(&asset))
    }

}
```

Make sure your `create-fungible-token-on-aptos/sources/fungible_asset.move` file is updated with the latest code and then in your opened terminal, run the following commands:

```
git add .
git commit -m "code update"
git push
```

And this sets up us for finally going in and deploy our contract to the Aptos Network.

![aptos05-ezgif com-resize](https://github.com/user-attachments/assets/20b214cf-df5b-4460-93d2-b12501cf9dbc)


## That’s a Wrap

We've added some powerful functionalities to our token contract, including the ability to withdraw, deposit, and freeze/unfreeze accounts. These features give us enhanced control and flexibility in managing our tokens. Now, let's move on to deploying our token to the Testnet!
