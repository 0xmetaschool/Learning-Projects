# Creating the ERC721 Smart Contract

Hello everyone! Welcome to another lesson! This one is going to be super interesting since we are going to create our very own smart contract to mint NFTs. Sounds fun, right? So, let’s get straight into it. 

![aptos-c4-l10 (1).gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C3%2010k%20NFT%20Images/Lesson%207%20Creating%20the%20ERC721%20Smart%20Contract/aptos-c4-l10_(1).webp?raw=true)

## Set Up Your Project

Hardhat is our secret weapon for smart contract development! It simplifies everything from testing your code to deploying it on the blockchain. You can think of it as your blockchain toolkit. Well, instead of building everything from scratch, we have made it easy for you by providing a boilerplate code. This will help you to start right away~

Run the following command in your terminal:

```
gh repo fork https://github.com/0xmetaschool/10k-nft-boilderplate.git --clone 
```

This will clone the boilerplate code of your project onto your system and fork the repository to your Git account as well.

Now run these commands:

```
cd 10k-nft-boilderplate/backend 
npm install
```

This will download and install Hardhat, its dependencies, and other code libraries like `ether.js` that it needs to work.

## Configuring Hardhat for Core

Now, let's tell Hardhat about our new playground (the Core testnet). This will allow it to connect to the network and deploy our contracts on the Core testnet.

Create a `.env` file in `10k-nft-boilderplate/backend` folder and add the following content to it.

```
PRIVATE_KEY=""
```

We copied our private key, well, here we will use it. Paste the private key into your `.env` file in between the `“”`. And we are set!

![Frame 3560410.gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C3%2010k%20NFT%20Images/Lesson%205%20Setting%20up%20the%20project%20structure/address.webp?raw=true)

YOU ARE ALL SET!!!!

## Our Smart Contract

Navigate to `10k-nft-boilderplate/backend/contracts/NFT.sol`. We already have the code for you. Let’s understand it.

We will be using the ERC721 NFT smart contract from Openzeppelin. The contracts from  Openzeppelin are battle-tested which means, we do not have to worry much about the security of our smart contract as it is already taken care of. So let’s start building our smart contract:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;
```

- The SPDX license identifier is a way of specifying the license under which our contract's code is distributed. Here, `MIT` indicates that the code is licensed under the MIT License.
- The `pragma` directive is used to specify the version of the Solidity compiler that should be used to compile the contract.

```solidity
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/utils/Strings.sol";
```

- The first three import statements import the `ERC721` , `ERC721Enumerable`, and `ERC721URIStorage` contracts from the OpenZeppelin library.
- The next import is ownable which will be used when the owner has to have special rights.
- The last import `Ownable` statement imports the `Strings` library from the OpenZeppelin library. 
The `Strings` library provides functions for string operations, such as converting numbers to their string representation.

```solidity
contract CoreNFT is ERC721, ERC721Enumerable, ERC721URIStorage, Ownable {
```

- This line declares a new smart contract named `CoreNFT`.
- `is ERC721, ERC721Enumerable, ERC721URIStorage, Ownable` specifies that `CoreNFT` contract inherits from the `ERC721`, `ERC721Enumerable`, `ERC721URIStorage`, `Ownable` meaning, we can use all the functions from these contracts in our contract.

```solidity
       using Strings for uint256;
       uint256 private _nextTokenId;
       string private _baseURIExtended;
```

- `using Strings for uint256`: Enables `uint256` variables to use functions from the `Strings` library for converting numbers to strings.
- `uint256 private _nextTokenId`: Declares a variable to track the number of NFTs minted so far.
- `string _baseTokenURIExtended`: Declares a variable to store the base URI for NFT metadata. This is where all your NFT metadata is stored.

```solidity
constructor(string memory baseURI_)
        ERC721("CoreNFT", "CORE")
        Ownable(msg.sender)
    {
        _baseURIExtended = baseURI_;
				_nextTokenId = 0;
    }
```

When you first deploy the contract, it runs a special function called the constructor. This function runs only once during the deployment of the smart contract. This constructor in our contract does two main things. 

- It gives your NFT collection a name, "CoreNFT," and a short symbol, "Core," by calling the parent `ERC721` contract. It also sets the owner of the contract to your address since you deploy the contract.
- Second, it sets up a base URL, called `_baseURIExtended`, where the metadata for your NFTs will be stored and initializes `_nextTokenId` to 0 as well.

```jsx
    function setBaseURI(string memory baseURI_) external onlyOwner {
        _baseURIExtended = baseURI_;
    }
```

- This function, `setBaseURI`, allows the contract owner to set a new base URI for the NFT metadata. The `onlyOwner` modifier ensures that only the contract owner can call this function. The `baseURI_` parameter is a string that specifies the new base URI, and it updates the `_baseURIExtended` variable in the contract.

```solidity
    function safeMint(address to) public onlyOwner {
        uint256 tokenId = _nextTokenId++;
        _safeMint(to, tokenId);
        _setTokenURI(tokenId, string(abi.encodePacked(Strings.toString(tokenId), ".json")));
    }
```

- `uint256 tokenId = _nextTokenId++` increases the token ID and assigns it to `tokenId` so that you can go ahead and proceed to mint further
- The `_safeMint(msg.sender, tokenId);`  sends the NFT with the earlier set `tokenId` to `msg.sender` which is your address in this case.
- Finally, `_setTokenURI(tokenId, string(abi.encodePacked(Strings.toString(tokenId), ".json")))` sets the metadata URI for a specific NFT by converting the token ID to a string, and then adding `.json`, and storing this full string as the token's URI

```solidity
    function _baseURI() internal view virtual override returns (string memory) {
        return _baseURIExtended;
    }
```

The function `_baseURI()` in this contract overrides the base implementation from the ERC721 contract. Can you guess what it returns? It returns `_baseURIExtended`, which is the custom base URL for our NFT metadata. This is how the contract knows where to find the starting point for all the token metadata URLs.

```solidity
function getCurrentTokenId() public view returns (uint256) {
        return _nextTokenId;
```

This function returns the current TokenId. This also represents the number of NFTs minted so far. 

Lastly, we have the following functions which have to be overridden as required by Solidity.

```solidity
    function _update(address to, uint256 tokenId, address auth)
        internal
        override(ERC721, ERC721Enumerable)
        returns (address)
    {
        return super._update(to, tokenId, auth);
    }
    function _increaseBalance(address account, uint128 value)
        internal
        override(ERC721, ERC721Enumerable)
    {
        super._increaseBalance(account, value);
    }
    function tokenURI(uint256 tokenId)
        public
        view
        override(ERC721, ERC721URIStorage)
        returns (string memory)
    {
        string memory baseURI = _baseURI();
        return bytes(baseURI).length > 0 ? string(abi.encodePacked(baseURI, tokenId.toString(), ".json")) : "";
    }
    function supportsInterface(bytes4 interfaceId)
        public
        view
        override(ERC721, ERC721Enumerable, ERC721URIStorage)
        returns (bool)
    {
        return super.supportsInterface(interfaceId);
    }
} 
```

## Complete code

Now that we are done, let’s see what the complete code looks like:

```jsx
// SPDX-License-Identifier: MIT
// Compatible with OpenZeppelin Contracts ^5.0.0
pragma solidity ^0.8.20;
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/utils/Strings.sol";
contract CoreNFT is ERC721, ERC721Enumerable, ERC721URIStorage, Ownable {
       using Strings for uint256;
       uint256 private _nextTokenId;
       string private _baseURIExtended;
   constructor(string memory baseURI_) ERC721("CoreNFT", "CORE") Ownable(msg.sender) {
        _baseURIExtended = baseURI_;
        _nextTokenId = 0;
    }
    function setBaseURI(string memory baseURI_) external onlyOwner {
        _baseURIExtended = baseURI_;
    }
    function safeMint(address to) public onlyOwner {
        uint256 tokenId = _nextTokenId++;
        _safeMint(to, tokenId);
        _setTokenURI(tokenId, string(abi.encodePacked(Strings.toString(tokenId), ".json")));
    }
    function _baseURI() internal view virtual override returns (string memory) {
        return _baseURIExtended;
    }
    function getCurrentTokenId() public view returns (uint256) {
        return _nextTokenId;
    }
    // The following functions have to be overridden as required by Solidity.
    function _update(address to, uint256 tokenId, address auth)
        internal
        override(ERC721, ERC721Enumerable)
        returns (address)
    {
        return super._update(to, tokenId, auth);
    }
    function _increaseBalance(address account, uint128 value)
        internal
        override(ERC721, ERC721Enumerable)
    {
        super._increaseBalance(account, value);
    }
    function tokenURI(uint256 tokenId)
        public
        view
        override(ERC721, ERC721URIStorage)
        returns (string memory)
    {
        string memory baseURI = _baseURI();
        return bytes(baseURI).length > 0 ? string(abi.encodePacked(baseURI, tokenId.toString(), ".json")) : "";
    }
    function supportsInterface(bytes4 interfaceId)
        public
        view
        override(ERC721, ERC721Enumerable, ERC721URIStorage)
        returns (bool)
    {
        return super.supportsInterface(interfaceId);
    }
} 
```

## Wrap up

Congratulations!! You have successfully understood your own NFT contract to mint NFT. Keep up the pace!  There is so much more to learn, but we are sure that you are going to have a lot of fun in the upcoming lessons while we deploy and interact with our contract! So, see you there!