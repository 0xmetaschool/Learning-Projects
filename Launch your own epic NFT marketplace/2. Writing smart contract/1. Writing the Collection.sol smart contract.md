# Writing the Collection.sol smart contract

Go to your contracts directory and make a new file called `Collection.sol`.

![Frame 3560365 (3).gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Launch%20your%20own%20epic%20NFT%20place/2%20Writing%20smart%20contract/1.%20Writing%20the%20Collection%20sol%20smart%20contract/Frame_3560365_(3).webp?raw=true)

Populate this file with the following code:

```
// Contract based on [https://docs.openzeppelin.com/contracts/3.x/erc721](https://docs.openzeppelin.com/contracts/3.x/erc721)
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";

contract Collection is ERC721URIStorage, Ownable {

    uint256 private _tokenIds = 0;
    uint256 private _totalMinted = 0;
    mapping(address => uint8) private mintedAddress;
    mapping(string => uint8) private URIMapping;
    uint256 public PRICE_PER_TOKEN = 0.01 ether;
    uint256 public LIMIT_PER_ADDRESS = 2;
    uint256 public MAX_SUPPLY  = 5;

    constructor() ERC721("Collection", "NFT") Ownable(msg.sender) {}

    function setPrice(uint256 price) external onlyOwner{
        PRICE_PER_TOKEN = price;
    }

    function setLimit(uint256 limit) external onlyOwner{
        LIMIT_PER_ADDRESS = limit;
    }

    function setMaxSupply(uint256 max_supply) external onlyOwner{
        MAX_SUPPLY = max_supply;
    }

    function mintNFT(string memory tokenURI)
        payable
        external
        returns (uint256)
    {
        require(PRICE_PER_TOKEN <= msg.value, "Ether paid is incorrect");
        require(mintedAddress[msg.sender] < LIMIT_PER_ADDRESS, "You have exceeded minting limit");
        require(_totalMinted + 1 <= MAX_SUPPLY, "You have exceeded Max Supply");
        require(URIMapping[tokenURI] == 0, "This NFT has already been minted");
        URIMapping[tokenURI] += 1;
        mintedAddress[msg.sender] += 1;

        uint256 newItemId = _tokenIds;
        _mint(msg.sender, newItemId);
        _setTokenURI(newItemId, tokenURI);

        _tokenIds++;
        _totalMinted++;
        return newItemId;
    }

    function withdrawMoney() external onlyOwner{
        address payable to = payable(msg.sender);
        to.transfer(address(this).balance);
    }
}
```

Okay I know this is a lot of code, but it really isn’t that complicated once you start understanding it.

So let’s go through it step by step.

We import 3 contracts from openzeppelin:

1. `ERC721` - Standard implementation of the ERC721 smart contract used for making NFTs
2. `Ownable` - Allows us to use the `onlyOwner` modifier which in turn allows us to create functions that only the owner of the contract can use.
3. `ERC721URIStorage` - This is an extension of the ERC721 smart contract, that provides a way to store the metadata in an array list on chain using the `_setTokenURI` function.

## Creating the `mint` function

In the `mint` function you should notice these things -

1. We take in a parameter called the `tokenURI`. This is a link to the metadata of our NFT. A metadata is like the soul of our NFT. Your NFT is unique because of the metadata it has. It contains the complete information about the NFT like image or description. We will come back to the metadata soon, when we talk about IPFS hosting.
2. We increment our token ID for storing the NFTs metadata and minting the NFT using the following piece of code, we also increment the total Number of NFTs minted so that we can check it does not exceed our total supply.
    
    ```
    _tokenIds.increment();
    _totalMinted.increment();
    uint256 newItemId = _tokenIds.current();
    ```
    
3. We call the internal `_mint()` function to mint the NFT with the new token ID and airdropping it to our public address - `msg.sender`.

We also store the `tokenURI` in an array at the index of the new `tokenID` using `_setTokenURI()`

## Quick detour about this magical word - `msg`

`msg` provides us with a set of incredibly useful global variables, that we can use to extract important information from the blockchain.

We will be using the following global variables in this tutorial:

1. `msg.sender` - Gives us the public address of the entity which made the function call.
2. `msg.value` - Gives us the amount of ether sent (in wei) along with the function call

There are also other special variables like `msg.data`, `msg.gas` and `msg.sig` which you check out in the solidity documentation [Solidity Docs](https://docs.soliditylang.org/en/develop/units-and-global-variables.html?highlight=special%20variables%20and%20functions#block-and-transaction-properties).

While you absorb everything I have mentioned so far. I will explain other functions in detail in next lesson.