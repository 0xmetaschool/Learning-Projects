# Writing Your Token Smart Contract

Welcome back!  So we completed learning about tokens. Awesome, you did great! Well, get ready because in this lesson, we're going to learn how to write a token using clarity! Are you excited? Let's get started!

Like ERC20 used by EVM chains as their token standard, token contracts in clarity have a specific standard to follow! let’s dig deep…

## Traits

Think of traits as handy blueprints or templates for smart contracts. They serve as a way to organize and define a set of public functions that have specific names, input types, and output types. The whole idea behind a trait is to establish a clear and public interface that a contract can adhere to. Similar to Solidity's interfaces, traits provide a means to define a standardized set of functionalities that contracts can implement.

For example, we have a contract `contract-one` having a trait names `trait-old` and it is deployed on mainnet. Another contract `contract-two` can point to `contract-one.trait-old`. Traits are super important when it comes to making calls between different parts of a computer program.

### Define and implement trait

We can define and implement a trait as:

```
;; Define a trait
(define-trait adder
    (
        (add (uint uint) (response uint uint))
    )
)
```

In the above code, we are creating a trait called `adder` with a public function. Now you need to deploy this trait like you deploy a contract:

![stacks-1.gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Create%20a%20Token%20in%20Clarity%20on%20Stacks%20Blockchain/3.%20Let%E2%80%99s%20Start%20The%20More%20Fun%20Part/Writing%20Your%20Token%20Smart%20Contract%20c88f4ac6801f40beb2e91e30d251a803/stacks-1.webp?raw=true)

Open your wallet and go to the Explorer to view the contract address: 

![Stacks-2.gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Create%20a%20Token%20in%20Clarity%20on%20Stacks%20Blockchain/3.%20Let%E2%80%99s%20Start%20The%20More%20Fun%20Part/Writing%20Your%20Token%20Smart%20Contract%20c88f4ac6801f40beb2e91e30d251a803/Stacks-2.webp?raw=true)

Here the structure of the trait contract address is `<wallet address>.<name of the contract>`

Now we can implement this adder trait like this in our contract:

```jsx
(impl-trait 'ST30V7KNBYVX3C1F0HRRPASKS2JW16Q71KPSNN7MY.adder)
```

Here the `impl-trait`  is a function call that declares that the contract implements the trait named `adder` 

### Now Let’s break down the token code for you:

Let's start building the code line-by-line:

First go to [Sandbox](https://explorer.hiro.so/sandbox/deploy?chain=testnet) and paste deploy the following SIP-010 trait:

```jsx
(define-trait sip010-ft-trait
  (
    ;; Transfer from the caller to a new principal
    (transfer (uint principal principal (optional (buff 34))) (response bool uint))

    ;; the human readable name of the token
    (get-name () (response (string-ascii 32) uint))

    ;; the ticker symbol, or empty if none
    (get-symbol () (response (string-ascii 32) uint))

    ;; the number of decimals used, e.g. 6 would mean 1_000_000 represents 1 token
    (get-decimals () (response uint uint))

    ;; the balance of the passed principal
    (get-balance (principal) (response uint uint))

    ;; the current total supply (which does not need to be a constant)
    (get-total-supply () (response uint uint))

    ;; an optional URI that represents metadata of this token
    (get-token-uri () (response (optional (string-utf8 256)) uint))
    )
  )
```

![Screen Recording 2023-06-14 at 5.05.30 PM.gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Create%20a%20Token%20in%20Clarity%20on%20Stacks%20Blockchain/3.%20Let%E2%80%99s%20Start%20The%20More%20Fun%20Part/Writing%20Your%20Token%20Smart%20Contract%20c88f4ac6801f40beb2e91e30d251a803/Screen_Recording_2023-06-14_at_5.05.30_PM.webp?raw=true)

Once the code is deployed now you need to copy the contract address from the explorer, just check your Hiro Wallet activity after you sign the transaction, you will find the transaction link from where you will get the contract address of the trait you just deployed. In my case its `ST30V7KNBYVX3C1F0HRRPASKS2JW16Q71KPSNN7MY.sip-010-trait-ft-standard` 

(Well you can use the trait I deployed with your token contract, but remember its important to deploy and learn how to do it on your own)

![Screen Shot 2023-06-14 at 5.10.00 PM.png](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Create%20a%20Token%20in%20Clarity%20on%20Stacks%20Blockchain/3.%20Let%E2%80%99s%20Start%20The%20More%20Fun%20Part/Writing%20Your%20Token%20Smart%20Contract%20c88f4ac6801f40beb2e91e30d251a803/Screen_Shot_2023-06-14_at_5.10.00_PM.webp?raw=true)

```
(impl-trait 'ST30V7KNBYVX3C1F0HRRPASKS2JW16Q71KPSNN7MY.sip-010-trait-ft-standard)
```

This line indicates that this code implements a trait. `sip-010-trait-ft-standard` is just name of the contract and `ST30V7KNBYVX3C1F0HRRPASKS2JW16Q71KPSNN7MY` is the address of your wallet .

```
(define-constant contract-owner tx-sender)

```

Here, we define a constant variable named `contract-owner` and assign it the value of the transaction sender. It represents the owner of the contract.

```
(define-constant err-owner-only (err u100))
(define-constant err-not-token-owner (err u101))
```

These lines define two error constants, `err-owner-only` and `err-not-token-owner`. These constants are used to represent specific error conditions that may occur during contract execution.

```
(define-fungible-token clarity-coin)
```

This line defines a fungible token named `clarity-coin`. 

```
(define-public (transfer (amount uint) (sender principal) (recipient principal) (memo (optional (buff 34))))
    (begin
        (asserts! (is-eq tx-sender sender) err-not-token-owner)
        (try! (ft-transfer? clarity-coin amount sender recipient))
        (match memo to-print (print to-print) 0x)
        (ok true)))
```

Here, we define a public function named `transfer` that allows users to transfer tokens. It takes in the amount to be transferred, the sender and recipient addresses, and an optional memo. Inside the function, we perform several actions such as asserting that the transaction sender is the token owner, attempting to transfer the specified amount of tokens using the `ft-transfer` function, . Here the match function check if a memo exists or not if not it returns 0x.

```
(define-read-only (get-name)
    (ok "Clarity Coin"))
```

This function, `get-name`, is a read-only function that returns the name of the token, which in this case is `Clarity Coin`.

```
(define-read-only (get-symbol)
    (ok "CC"))
```

Similarly, the `get-symbol` function is a read-only function that returns the symbol of the token, which is `CC`.

```
(define-read-only (get-decimals)
    (ok u6))
```

The `get-decimals` function returns the number of decimal places for token balances, which in this case is represented by `u6`.

```
(define-read-only (get-balance (who principal))
    (ok (ft-get-balance clarity-coin who)))
```

This read-only function, `get-balance`, takes a principal address as an argument and returns the token balance of that address using the `ft-get-balance` function.

```
(define-read-only (get-total-supply)
    (ok (ft-get-supply clarity-coin)))
```

The `get-total-supply` function is a read-only function that returns the total token supply using the `ft-get-supply` function.

```
(define-read-only (get-token-uri)
    (ok none))
```

This function, `get-token-uri`, is a read-only function that returns the URI associated with the token. In this case, it returns `none` since there is no token URI defined.

## Complete code

The complete code is: 

```
(impl-trait 'ST30V7KNBYVX3C1F0HRRPASKS2JW16Q71KPSNN7MY.sip-010-trait-ft-standard)

;; Defines a constant "contract-owner" and assigns it 
;; the value of the transaction sender.
(define-constant contract-owner tx-sender)

;; Defines an error constant "err-owner-only" with a 
;; value of "u100" (user-defined error code).
(define-constant err-owner-only (err u100))

;; Defines an error constant "err-not-token-owner" with 
;; a value of "u101" (user-defined error code).
(define-constant err-not-token-owner (err u101))

;; Defines a fungible token named "clarity-coin".
(define-fungible-token clarity-coin)

;; Defines a public function "transfer" for transferring tokens 
;; between senders and recipients.
(define-public (transfer (amount uint) (sender principal) (recipient principal) (memo (optional (buff 34))))
    (begin
        ;; Checks if the transaction sender is the token owner; 
				;; otherwise, throws an error.
        (asserts! (is-eq tx-sender sender) err-not-token-owner)
        ;; Tries to transfer the specified amount of tokens from 
				;; the sender to the recipient.
        (try! (ft-transfer? clarity-coin amount sender recipient))
        ;; Matches the "memo" argument and prints it if it's a printable string.
        (match memo to-print (print to-print) 0x)
        ;; Returns "true" to indicate a successful transfer.
        (ok true)))

;; Defines a read-only function "get-name" to retrieve the name of the token.
(define-read-only (get-name)
    ;; Returns the name "Clarity Coin".
    (ok "Clarity Coin"))

;; Defines a read-only function "get-symbol" to retrieve the symbol of the token.
(define-read-only (get-symbol)
    ;; Returns the symbol "CC".
    (ok "CC"))

;; Defines a read-only function "get-decimals" to retrieve 
;; the decimal places for token balances.
(define-read-only (get-decimals)
    ;; Returns the number of decimal places as "u6".
    (ok u6))

;; Defines a read-only function "get-balance" to retrieve 
;; the token balance of a given principal.
(define-read-only (get-balance (who principal))
    ;; Returns the token balance for the specified principal.
    (ok (ft-get-balance clarity-coin who)))

;; Defines a read-only function "get-total-supply" to 
;; retrieve the total token supply.
(define-read-only (get-total-supply)
    ;; Returns the total token supply.
    (ok (ft-get-supply clarity-coin)))

;; Defines a read-only function "get-token-uri" to 
;; retrieve the URI associated with the token.
(define-read-only (get-token-uri)
    ;; Returns "none" as there is no token URI defined.
    (ok none))

;; Defines a public function "mint" to create new tokens 
;; and assign them to a recipient.
(define-public (mint (amount uint) (recipient principal))
    (begin
        ;; Checks if the transaction sender is the contract owner; 
				;; otherwise, throws an error.
        (asserts! (is-eq tx-sender contract-owner) err-owner-only)
        ;; Mints the specified amount of tokens and assigns 
				;; them to the recipient.
        (ft-mint? clarity-coin amount recipient)))
```

## Wrap up

In this lesson, we learned how to write your token in Clarity. We discussed the importance of traits and how they are similar to templates for smart contracts. We also went through the code line-by-line and explained how each function works. Finally, we provided the complete code for a basic token contract.

Now that we've learned about tokens and how to code them, we're ready to deploy and interact with them. Stay tuned for the next lesson!