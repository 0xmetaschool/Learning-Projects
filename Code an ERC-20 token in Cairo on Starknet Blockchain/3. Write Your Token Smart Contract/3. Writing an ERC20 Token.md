# Writing an ERC20 Token

Welcome back! So you have set up your development environment. Well, get ready because in this lesson, we're going to learn how to write ERC20 token, maybe wait…let’s call our token PEPE Token! Are you excited? Let's get started!

## What is an ERC20 token

ERC20 tokens are fungible tokens and are replaceable by another token of the same type, just like money. For example, a $10 bill in one wallet holds the same value and can be exchanged for another $10 bill. 

## Building the contract

## Create a file

Let’s create a file named `erc20.cairo` in `src/contract`:

![Frame 3560339 (4).gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/Code%20an%20ERC-20%20token%20in%20Cairo%20on%20Starknet%20Blockchain/assests/L4_1_updated_create_new_file%20GIF.gif?raw=true)
### Essential import

We will use `ContractAddress`. The following line imports the `ContractAddress` type from the `starknet` module. It allows us to use the `ContractAddress` type in our code.

```
 use starknet::ContractAddress;
```

### Define trait

We will define a trait. It will provide us a blueprint for a set of methods that ERC20 will implement. We will use `trait` keyword to define it and then mention the signature of functions inside parentheses {}.

```
trait IERC20 {
    fn name() -> felt252;
    fn symbol() -> felt252;
    fn decimals() -> u8;
    fn total_supply() -> u256;
    fn balance_of(account: ContractAddress) -> u256;
    fn allowance(owner: ContractAddress, spender: ContractAddress) -> u256;
    fn transfer(recipient: ContractAddress, amount: u256);
    fn transfer_from(sender: ContractAddress, recipient: ContractAddress, amount: u256);
    fn approve(spender: ContractAddress, amount: u256);
}
```

The  `IERC20` is the name of trait. We can define a function using `fn`. Moreover, instead of `return` , `->` is used. `felt252` is used for short strings type in Cairo.

The trait lists several functions that an ERC20 token should implement. So let’s see the methods and what do they return:

- `fn name() -> felt252`; : returns token’s name
- `fn symbol() -> felt252;` : returns the symbol of token
- `fn decimals() -> u8;` : returns the decimals of token
- `fn total_supply() -> u256;` : returns the total supply of token
- `fn balance_of(account: ContractAddress) -> u256;` : queries and returns the balance of a specific account
- `fn allowance(owner: ContractAddress, spender: ContractAddress) -> u256;`: queries and returns the allowance assigned to a spender by an owner
- `fn transfer(recipient: ContractAddress, amount: u256);`: transfers a certain amount of tokens from a sender to the specified recipient
- `fn transfer_from(sender: ContractAddress, recipient: ContractAddress, amount: u256);`: allows a spender to transfer a certain amount of tokens allowed to be spent by the owner/sender to the specified recipient
- `fn approve(spender: ContractAddress, amount: u256);` : approves a spender to spend a certain amount of tokens from the owner’s wallet

Now we will add the code of implementation of our trait in the same file.

### Modules in Cairo

We will utilize modules in Cairo. The functionalities related to achieve a specific task are grouped together in a module. So we will define a module for our ERC20 token and use `mod` keyword for this.

```
#[contract]
mod ERC20 {
	//ERC20 token code
}
```
We will add all the following code discussed in the lesson in our module block.

### Imports in module

We will use some imports in our module too:

```
use starknet::get_caller_address;
use starknet::ContractAddress;
use super::IERC20;
```

1. **`use starknet::get_caller_address;`**: Import the **`get_caller_address`** function from the **`starknet`** module. This function returns the address of the caller (sender) of the contract.
2. **`use starknet::ContractAddress;`**: Import the **`ContractAddress`** type from the **`starknet`** module.
3. **`use super::IERC20;`**: Import the **`IERC20`** trait from the parent module.

### Define struct

Next we will define a struct named **`Storage`**. It contains fields for various data related to the token, such as name, symbol, decimals, total supply, balances, and allowances.

```
struct Storage {
        _name: felt252,
        _symbol: felt252,
        _decimals: u8,
        _total_supply: u256,
        _balances: LegacyMap<ContractAddress, u256>,
        _allowances: LegacyMap<(ContractAddress, ContractAddress), u256>,
    }
```

### Events

Events in Cairo are a useful tool for logging and notifying external entities about specific occurrences within a contract. We will define two events that we will use in the functions we define later on:

```
#[event]
fn Transfer(from: ContractAddress, to: ContractAddress, value: u256) {}

#[event]
fn Approval(owner: ContractAddress, spender: ContractAddress, value: u256) {}
```

### Constructor

We’re going to start with initializing the constructor of the contract. For this we will first write a constructor and then rest of our code:

```
// The #[constructor] tag indicates that this is the constructor function 
// for the contract.
#[constructor]
    fn constructor() {
        _name::write(379067928035501022078318); //Name of the token
        _symbol::write(1346719813); //Symbol of the token
        _decimals::write(8); //Decimal of the token
    }
```

Let’s understand the variables that we defined. First one is name, we need a name in hex. So, in our case we named our token as PEPE Token, so we need to convert it hex. But how?? Hey, I got you!

Just create a file named `util.py` under `cairo-token` folder and paste the following code to it:

```
MAX_LEN_FELT = 31
 
def str_to_felt(text):
    if len(text) > MAX_LEN_FELT:
        raise Exception("Text length too long to convert to felt.")
    return int.from_bytes(text.encode(), "big")
 
def felt_to_str(felt):
    length = (felt.bit_length() + 7) // 8
    return felt.to_bytes(length, byteorder="big").decode("utf-8")
 
def str_to_felt_array(text):
    return [str_to_felt(text[i:i+MAX_LEN_FELT]) for i in range(0, len(text), MAX_LEN_FELT)]
 
def uint256_to_int(uint256):
    return uint256[0] + uint256[1]*2**128
 
def uint256(val):
    return (val & 2**128-1, (val & (2**256-2**128)) >> 128)
 
def hex_to_felt(val):
    return int(val, 16)
```

Now run the following command in your terminal:

```
python3 -i util.py
```

And there you have a terminal looking like this:

![Screen Shot 2023-06-02 at 6.07.12 PM.png](https://github.com/0xmetaschool/Learning-Projects/blob/main/Code%20an%20ERC-20%20token%20in%20Cairo%20on%20Starknet%20Blockchain/assests/L5_8_util.jpg?raw=true)

Now let’s check what hex does it generate for our string `PEPE Token` run the following command:

```
str_to_felt("PEPE Token")
```

![Screen Shot 2023-06-02 at 6.09.27 PM.png](https://github.com/0xmetaschool/Learning-Projects/blob/main/Code%20an%20ERC-20%20token%20in%20Cairo%20on%20Starknet%20Blockchain/assests/L5_9_pepe_token.jpg?raw=true)

You have the first value, name, ready now i.e.: `379067928035501022078318`.

For `_symbol`, you can generate this using the same `util.py` file by running the `str_to_felt()` function. For PEPE it’d be `1346719813` .

For `_decimals`, we kept it `8`, we won’t change it!

> You should generate your unique token name and symbol and replace their values in the code.
    
Now run `exit()` to exit the terminal.

![Screen Shot 2023-06-02 at 6.19.11 PM.png](https://github.com/0xmetaschool/Learning-Projects/blob/main/Code%20an%20ERC-20%20token%20in%20Cairo%20on%20Starknet%20Blockchain/assests/L5_10_exit.jpg?raw=true)

Now let’s come back to `erc20.cairo` file.

## Functions in Cairo

Unlike Solidity, Cairo does not specify visibility using public, internal, external, or private keywords. Instead it uses the attributes of `[view]`, or `[external]`. By default all functions are internal. View functions are getter functions. While `[external]` functions can be called by users via blockchain transaction. They consume gas fee as they are able to modify state of contract.

## Implementing other functions

So now let’s start implementing the functions and ERC20 token contract. Remember that we will be writing our code inside the module we have created.

### Name

It is a view function that returns the name of token upon querying. Yes, you might have guessed it right, we are not changing blockchain’s state so we used a `view` function.

```
#[view]
fn name() -> felt252 {
		_name::read()
}
```

### Symbol

It is a view function. It returns the token’s symbol upon querying.

```
#[view]
fn symbol() -> felt252 {
		_symbol::read()
}
```

### Decimals

It is a view function. It returns the token’s decimals upon querying.

```
#[view]
fn decimals() -> u8 {
		_decimals::read()
}
```

### Total supply

It is a view function. It returns the total supply of token.

```
#[view]
fn total_supply() -> u256 {
		_total_supply::read()
}
```

### BalanceOf

It is a view function. It returns the total amount owned by a particular account upon querying. The argument is the account number of the wallet whose balance we want to know.

```
#[view]
fn balance_of(account: ContractAddress) -> u256 {
		_balances::read(account)
}
```

### Allowance

It is a view function. It queries and returns the amount of tokens assigned to a spender by an owner.

```
#[view]
fn allowance(owner: ContractAddress, spender: ContractAddress) -> u256 {
		_allowances::read((owner, spender))
}
```

### Mint

The `mint` function is an external function. It is used to create new tokens and add them to the total supply.

```
#[external]
fn mint(amount: u256) {
		let sender = get_caller_address();

		_total_supply::write(_total_supply::read() + amount);
		_balances::write(sender, _balances::read(sender) + amount);
}
```

### Approve

It approves a spender to spend a certain amount of tokens from the wallet of owner.  

```
#[external]
fn approve(spender: ContractAddress, amount: u256) {
    let owner = get_caller_address();
		_allowances::write((owner, spender), amount);
    Approval(owner, spender, amount);
}
```

### Transfer

It is an external function. It initiates a transfer of an amount to the specified recipient. 

```
#[external]
fn transfer(to: ContractAddress, amount: u256) {
    let from = get_caller_address();
		_balances::write(from, _balances::read(from) - amount);
    _balances::write(to, _balances::read(to) + amount);
		Transfer(from, to, amount);
}
```

### TransferFrom

This function enables a spender to transfer a specific amount of tokens that are authorized by the owner/sender to the designated recipient. For example, now I can send say $10 to Alice using my account to hers.

```
#[external]
fn transfer_from(from: ContractAddress, to: ContractAddress, amount: u256) {
		let caller = get_caller_address();
		let allowed: u256 = _allowances::read((from, caller));

		let ONES_MASK = 0xffffffffffffffffffffffffffffffff_u128;

		let is_max = (allowed.low == ONES_MASK) & (allowed.high == ONES_MASK);

		if !is_max {
				_allowances::write((from, caller), allowed - amount);
        Approval(from, caller, allowed - amount);
    }

    _balances::write(from, _balances::read(from) - amount);
    _balances::write(to, _balances::read(to) + amount);

    Transfer(from, to, amount);
}
```

## Complete code

The complete code to be added in `erc20.cairo` is below:

```
use starknet::ContractAddress;

trait IERC20 {
    fn name() -> felt252;
    fn symbol() -> felt252;
    fn decimals() -> u8;
    fn total_supply() -> u256;
    fn balance_of(account: ContractAddress) -> u256;
    fn allowance(owner: ContractAddress, spender: ContractAddress) -> u256;
    fn transfer(recipient: ContractAddress, amount: u256);
    fn transfer_from(sender: ContractAddress, recipient: ContractAddress, amount: u256);
    fn approve(spender: ContractAddress, amount: u256);
}

#[contract]
mod ERC20 {
    use starknet::get_caller_address;
    use starknet::ContractAddress;
    use super::IERC20;

    struct Storage {
        _name: felt252,
        _symbol: felt252,
        _decimals: u8,
        _total_supply: u256,
        _balances: LegacyMap<ContractAddress, u256>,
        _allowances: LegacyMap<(ContractAddress, ContractAddress), u256>,
    }

    #[event]
    fn Transfer(from: ContractAddress, to: ContractAddress, value: u256) {}

    #[event]
    fn Approval(owner: ContractAddress, spender: ContractAddress, value: u256) {}

    #[constructor]
    fn constructor() {
        _name::write(379067928035501022078318); //Name of the token
        _symbol::write(1346719813); //Symbol of the token
        _decimals::write(8); //Decimal of the token
    }

    #[view]
    fn name() -> felt252 {
        _name::read()
    }

    #[view]
    fn symbol() -> felt252 {
        _symbol::read()
    }

    #[view]
    fn decimals() -> u8 {
        _decimals::read()
    }

    #[view]
    fn total_supply() -> u256 {
        _total_supply::read()
    }

    #[view]
    fn balance_of(account: ContractAddress) -> u256 {
        _balances::read(account)
    }

    #[view]
    fn allowance(owner: ContractAddress, spender: ContractAddress) -> u256 {
        _allowances::read((owner, spender))
    }

    #[external]
    fn approve(spender: ContractAddress, amount: u256) {
        let owner = get_caller_address();

        _allowances::write((owner, spender), amount);

        Approval(owner, spender, amount);
    }

    #[external]
    fn mint(amount: u256) {
        let sender = get_caller_address();

        _total_supply::write(_total_supply::read() + amount);
        _balances::write(sender, _balances::read(sender) + amount);
    }

    #[external]
    fn transfer(to: ContractAddress, amount: u256) {
        let from = get_caller_address();

        _balances::write(from, _balances::read(from) - amount);
        _balances::write(to, _balances::read(to) + amount);

        Transfer(from, to, amount);
    }

    #[external]
    fn transfer_from(from: ContractAddress, to: ContractAddress, amount: u256) {
        let caller = get_caller_address();
        let allowed: u256 = _allowances::read((from, caller));

        let ONES_MASK = 0xffffffffffffffffffffffffffffffff_u128;

        let is_max = (allowed.low == ONES_MASK) & (allowed.high == ONES_MASK);

        if !is_max {
            _allowances::write((from, caller), allowed - amount);
            Approval(from, caller, allowed - amount);
        }

        _balances::write(from, _balances::read(from) - amount);
        _balances::write(to, _balances::read(to) + amount);

        Transfer(from, to, amount);
    }
}
```

## Wrap up

In this lesson, we completed writing ERC20 token in Cairo programming language.

In the next lesson, we will deploy it on the Starknet blockchain.
