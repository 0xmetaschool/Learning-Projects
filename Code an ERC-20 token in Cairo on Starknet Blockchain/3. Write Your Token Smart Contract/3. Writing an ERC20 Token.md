# Writing an ERC20 Token

Welcome back! So you have set up your development environment. Well, get ready because in this lesson, we're going to learn how to write ERC20 token, maybe wait…let’s call our token PEPE Token! Are you excited? Let's get started!

## What is an ERC20 token

ERC20 tokens are fungible tokens and are replaceable by another token of the same type, just like money. For example, a $10 bill in one wallet holds the same value and can be exchanged for another $10 bill. 

## Building the contract

### Create a file

Create a file named `erc20.cairo` in `src` folder:

![Frame 3560364 (20).jpg](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/assests_for_starknet/Writing%20an%20ERC20%20Token/Frame_3560364_(20).webp?raw=true)

### Let’s start coding

Let’s begin writing code for our ERC20 token:

```
#[starknet::contract]
mod cairo_token {
```

- This line defines a new Starknet contract module named `cairo_token`.

```
    use starknet::ContractAddress;
    use starknet::get_caller_address;
```

- `starknet::ContractAddress`: This refers to the `ContractAddress` type from the `starknet` module. A `ContractAddress` is a unique identifier for a contract deployed on the Starknet blockchain.
- `starknet::get_caller_address`: This is importing the `get_caller_address` function from the `starknet` module. In the context of smart contract execution on a blockchain like Starknet, the "caller" typically refers to the account or contract that initiates a function call to the current contract. The `get_caller_address` function would provide the address of the caller, allowing the contract to determine who is invoking its functions.

```
    #[storage]
    struct Storage {
        owner: ContractAddress,
        name: felt252,
        symbol: felt252,
        total_supply: u256,
        decimal: u8,
        balances: LegacyMap::<ContractAddress, u256>,
        allowances: LegacyMap::<(ContractAddress, ContractAddress), u256>, 
    }
```

- `#[storage]`:  `Storage` struct is used to define the contract's storage layout. Smart contract storage holds persistent data that is maintained across function calls and transactions.
- `struct Storage { ... }`: This defines a struct named `Storage` which represents the storage layout for the smart contract. It defines various fields that store contract-related data.
- `owner: ContractAddress`: This field named `owner` holds an instance of `ContractAddress`. It represents the owner's address for the smart contract. The `ContractAddress` type is provided by the `starknet` module, and it's used to uniquely identify a contract on the Starknet blockchain.
- `name: felt252` and `symbol: felt252`: These fields hold instances of the `felt252` type. These fields store the name and symbol of the token represented by the contract.
- `total_supply: u256` and `decimal: u8`: These fields hold instances of the `u256` and `u8` types respectively. `u256` represents an unsigned 256-bit integer, and `u8` represents an unsigned 8-bit integer. These fields are used to store the total supply and decimal information of the token.
- `balances: LegacyMap::<ContractAddress, u256>`: This field is using a type called `LegacyMap` to store account balances. It's a map-like data structure where keys are of type `ContractAddress` and values are of type `u256`, representing the balance of each account.
- `allowances: LegacyMap::<(ContractAddress, ContractAddress), u256>`: This field, similar to `balances`, uses a `LegacyMap` to store allowance values. The keys are pairs of `ContractAddress` instances representing owner and spender addresses, and the values are of type `u256`, indicating the allowed amount.

```
#[constructor]
    fn constructor(ref self: ContractState, _owner:ContractAddress, _name: felt252, _symbol: felt252, _decimal: u8, _total_supply: u256) {
        self.name.write(_name);
        self.symbol.write(_symbol);
        self.decimal.write(_decimal);
        self.owner.write(_owner);

    }
```

- `#[constructor]`: This attribute marks a function as the constructor for the smart contract. A constructor is called when a new instance of the contract is created and deployed.
- `fn constructor(ref self: ContractState, ... ) { ... }`: This defines the constructor function, which takes several parameters and initializes the contract's storage.
- `ref self: ContractState`: The `self` parameter is a reference to the contract's state (storage). `ContractState` is a type that represents the contract's state.
- `_owner: ContractAddress, _name: felt252, _symbol: felt252, _decimal: u8, _total_supply: u256`: These are the input parameters expected by the constructor. They represent values that need to be passed when deploying the contract, such as the owner's address, token name, symbol, decimal, and total supply.
- `self.name.write(_name);`, `self.symbol.write(_symbol);`, etc.: These lines are storing the input values `_name`, `_symbol`, `_decimal`, and `_owner` into their respective fields within the contract's storage. The `write` operation is used to update the values in the storage.

```
#[external(v0)]
    #[generate_trait]
    impl CairoTokenTraitImpl of CairoTokenTrait {
```

- `#[external(v0)]`: This attribute marks a function as an external entry point for the smart contract. It indicates that this function can be called from outside the contract, such as by other contracts or external entities.
- `#[generate_trait]`: This attribute specifies that the following implementation block will be generating an implementation for a specific trait.
- `impl CairoTokenTraitImpl of CairoTokenTrait`: It's generating an implementation of a trait named `CairoTokenTraitImpl` based on the `CairoTokenTrait` trait.

```
fn name(self: @ContractState) -> felt252 {
            self.name.read()
        }

        fn owner(self: @ContractState) -> ContractAddress {
            self.owner.read()
        }

        fn symbol(self: @ContractState) -> felt252 {
            self.symbol.read()
        }

        fn totalSupply(self: @ContractState) -> u256 {
            self.total_supply.read()
        }
```

- `fn name(self: @ContractState) -> felt252`: This function, when called on a contract instance, reads and returns the stored token name (`name`) from the contract's storage.
- `fn owner(self: @ContractState) -> ContractAddress`: When invoked on a contract instance, this function retrieves and returns the owner's address (`owner`) from the contract's storage.
- `fn symbol(self: @ContractState) -> felt252`: When used on a contract instance, this function reads and provides the stored token symbol (`symbol`) from the contract's storage.
- `fn totalSupply(self: @ContractState) -> u256`: When called on a contract instance, this function retrieves and returns the total supply value (`total_supply`) of the token from the contract's storage.

```
fn mint(ref self: ContractState, to: ContractAddress, amount: u256) {
            assert(get_caller_address() == self.owner.read(), 'Invalid caller');
            let new_total_supply = self.total_supply.read() + amount;
            self.total_supply.write(new_total_supply);
            let new_balance = self.balances.read(to) + amount;
            self.balances.write(to, new_balance);
        }
```

The `mint` function creates and assigns new tokens to a specified address:

- `fn mint(ref self: ContractState, to: ContractAddress, amount: u256)`: This function allows the contract's owner to create new tokens and assign them to a specified address.
- `assert(get_caller_address() == self.owner.read(), 'Invalid caller')`: Before minting, the function checks if the caller (invoker) is the contract owner. If not, the operation is halted with an error message.
- `let new_total_supply = self.total_supply.read() + amount;`: The function calculates the new total supply by adding the specified `amount` of tokens to the existing total supply.
- `self.total_supply.write(new_total_supply);`: The updated total supply is written back to the contract's storage.
- `let new_balance = self.balances.read(to) + amount;`: The function calculates the new balance for the specified address by adding the `amount` to the current balance.
- `self.balances.write(to, new_balance);`: The updated balance is written back to the contract's storage for the specified address.

```
 fn transfer(ref self: ContractState, to: ContractAddress, amount: u256){
    let caller: ContractAddress = get_caller_address();
    self._transfer(caller, to, amount);
}
```

- This function enables transferring tokens from the caller's address to a specified address. It utilizes the internal `_transfer` function to handle the actual transfer.

```
fn transferFrom(ref self: ContractState, sender: ContractAddress, to: ContractAddress, amount: u256){
     let caller = get_caller_address();
     assert(self.allowances.read((sender, caller)) >= amount, 'No allowance');
     self.allowances.write((sender, caller), self.allowances.read((sender, caller)) - amount);
     self._transfer(sender, to, amount);
}
```

This function allows transferring tokens from a specified sender's address to another address, subject to an allowance. It checks the allowance, updates it, and uses `_transfer` to perform the transfer.

- `fn transferFrom(...)`: Function to transfer tokens from `sender` to `to`, based on an allowance.
- `ref self: ContractState`: Reference to contract's state.
- `sender: ContractAddress`, `to: ContractAddress`, `amount: u256`: Parameters for the transfer.
- `let caller = get_caller_address();`: Get caller's address.
- `assert(self.allowances.read((sender, caller)) >= amount, 'No allowance');`: Check if caller has sufficient allowance from sender.
- `self.allowances.write((sender, caller), self.allowances.read((sender, caller)) - amount);`: Update allowance by subtracting transferred amount.
- `self._transfer(sender, to, amount);`: Call `_transfer` for the actual token transfer.

```
fn approve(ref self: ContractState, spender: ContractAddress, amount: u256) {
  let caller: ContractAddress = get_caller_address();
  let mut prev_allowance: u256 = self.allowances.read((caller, spender));
  self.allowances.write((caller, spender), prev_allowance + amount);
}
```

- This function lets the caller grant an allowance to another address (`spender`) to spend a certain amount of tokens from their balance.
- `let caller: ContractAddress = get_caller_address();`: This line uses the `get_caller_address()` function from the `starknet` module to determine the address of the function caller. The caller's address is assigned to the variable `caller`.
- `let mut prev_allowance: u256 = self.allowances.read((caller, spender));`: This line reads the current allowance value that the caller has granted to the specified `spender`. The `allowances` storage field holds a map of allowances, and this line retrieves the existing allowance for the `(caller, spender)` pair. The `mut` keyword indicates that this variable can be modified.
- `self.allowances.write((caller, spender), prev_allowance + amount);`: This line updates the allowance in the contract's storage. It increases the existing allowance by the specified `amount` and then writes the new allowance back to the storage. This effectively allows the `spender` to spend more tokens on behalf of the caller.

```
fn allowance(self: @ContractState, owner: ContractAddress, spender: ContractAddress) -> u256 {
	self.allowances.read((owner, spender))
}

fn balanceOf(self: @ContractState, account: ContractAddress) -> u256 {
	self.balances.read(account)
}
```

- `allowance` function returns the allowance granted by the owner to the spender. It reads the allowance from the contract's storage.
- `balanceOf` function retrieves and returns the token balance of a specified account from the contract's storage.

```
#[generate_trait]
    impl PrivateFunctions of CairoTokenPrivateFunctionsTrait {

        fn _transfer(ref self: ContractState, sender: ContractAddress, recipient: ContractAddress, amount: u256) {
            assert(self.balances.read(sender) >= amount, 'Insufficient bal');
            self.balances.write(recipient, self.balances.read(recipient) + amount);
            self.balances.write(sender, self.balances.read(sender) - amount)
        }

    }

}
```

- `#[generate_trait]`: This attribute indicates that the following implementation block will generate an implementation for a trait.
- `impl PrivateFunctions of CairoTokenPrivateFunctionsTrait {`: This part begins an implementation block for the `PrivateFunctions` trait, using the `CairoTokenPrivateFunctionsTrait` trait.
- `fn _transfer(ref self: ContractState, sender: ContractAddress, recipient: ContractAddress, amount: u256) {`:
    - This line defines a private function named `_transfer` which handles the actual token transfer process.
    - `ref self: ContractState`: The `self` parameter is a reference to the contract's state (storage). It's passed by reference (`ref`).
    - `sender: ContractAddress`, `recipient: ContractAddress`: These parameters represent the sender's and recipient's addresses for the token transfer.
    - `amount: u256`: This parameter specifies the amount of tokens being transferred.
- `assert(self.balances.read(sender) >= amount, 'Insufficient bal');`:
    - This line checks if the sender's balance is greater than or equal to the transfer amount.
    - If the condition is false, the assertion fails, and an 'Insufficient bal' message is displayed. This ensures that the sender has enough tokens to transfer.
- `self.balances.write(recipient, self.balances.read(recipient) + amount);`:
    - This line updates the recipient's balance by adding the transferred amount to their existing balance. It uses the `write` method to update the value in storage.
- `self.balances.write(sender, self.balances.read(sender) - amount)`:
    - This line updates the sender's balance by subtracting the transferred amount from their existing balance. It ensures the sender's balance is reduced by the transferred amount.

## Complete code

The complete code to be added in `erc20.cairo` is below:

```
#[starknet::contract]
mod cairo_token {

    use starknet::ContractAddress;
    use starknet::get_caller_address;

    #[storage]
    struct Storage {
        owner: ContractAddress,
        name: felt252,
        symbol: felt252,
        total_supply: u256,
        decimal: u8,
        balances: LegacyMap::<ContractAddress, u256>,
        allowances: LegacyMap::<(ContractAddress, ContractAddress), u256>, 
    }

    #[constructor]
    fn constructor(ref self: ContractState, _owner:ContractAddress, _name: felt252, _symbol: felt252, _decimal: u8, _total_supply: u256) {
        self.name.write(_name);
        self.symbol.write(_symbol);
        self.decimal.write(_decimal);
        self.owner.write(_owner);

    }

    #[external(v0)]
    #[generate_trait]
    impl CairoTokenTraitImpl of CairoTokenTrait {

        fn name(self: @ContractState) -> felt252 {
            self.name.read()
        }

        fn owner(self: @ContractState) -> ContractAddress {
            self.owner.read()
        }

        fn symbol(self: @ContractState) -> felt252 {
            self.symbol.read()
        }

        fn totalSupply(self: @ContractState) -> u256 {
            self.total_supply.read()
        }

        fn mint(ref self: ContractState, to: ContractAddress, amount: u256) {
            assert(get_caller_address() == self.owner.read(), 'Invalid caller');
            let new_total_supply = self.total_supply.read() + amount;
            self.total_supply.write(new_total_supply);
            let new_balance = self.balances.read(to) + amount;
            self.balances.write(to, new_balance);
        }

        fn transfer(ref self: ContractState, to: ContractAddress, amount: u256){
            let caller: ContractAddress = get_caller_address();
            self._transfer(caller, to, amount);
        }

        fn transferFrom(ref self: ContractState, sender: ContractAddress, to: ContractAddress, amount: u256){
            let caller = get_caller_address();
            assert(self.allowances.read((sender, caller)) >= amount, 'No allowance');
            self.allowances.write((sender, caller), self.allowances.read((sender, caller)) - amount);
            self._transfer(sender, to, amount);
        }

        fn approve(ref self: ContractState, spender: ContractAddress, amount: u256) {
            let caller: ContractAddress = get_caller_address();
            let mut prev_allowance: u256 = self.allowances.read((caller, spender));
            self.allowances.write((caller, spender), prev_allowance + amount);
        }

        fn allowance(self: @ContractState, owner: ContractAddress, spender: ContractAddress) -> u256 {
            self.allowances.read((owner, spender))
        }

        fn balanceOf(self: @ContractState, account: ContractAddress) -> u256 {
            self.balances.read(account)
        }
    }

    #[generate_trait]
    impl PrivateFunctions of CairoTokenPrivateFunctionsTrait {

        fn _transfer(ref self: ContractState, sender: ContractAddress, recipient: ContractAddress, amount: u256) {
            assert(self.balances.read(sender) >= amount, 'Insufficient bal');
            self.balances.write(recipient, self.balances.read(recipient) + amount);
            self.balances.write(sender, self.balances.read(sender) - amount)
        }

    }

}
```

## `util.py`

We need to send parameters like name and symbol of token in hex so we will utilize `util.py` file. Just create a file named `util.py` under your main folder (`metaschoolStarknet`) and paste the following code to it:

```
MAX_LEN_FELT = 31
 
def str_to_felt(text):
    if len(text) > MAX_LEN_FELT:
        raise Exception("Text length too long to convert to felt.")
    return int.from_bytes(text.encode(), "big")
 
def felt_to_str(felt):
    length = (felt.bit_length() + 7) // 8
    return felt.to_bytes(length, byteorder="big").decode("utf-8")
 
def str_to_felt_array(text):
    return [str_to_felt(text[i:i+MAX_LEN_FELT]) for i in range(0, len(text), MAX_LEN_FELT)]
 
def uint256_to_int(uint256):
    return uint256[0] + uint256[1]*2**128
 
def uint256(val):
    return (val & 2**128-1, (val & (2**256-2**128)) >> 128)
 
def hex_to_felt(val):
    return int(val, 16)
```

Let me show you my `util.py` file.

![Frame 3560365 (1).jpg](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/assests_for_starknet/Writing%20an%20ERC20%20Token/Frame_3560365_(1).webp?raw=true)

Now run the following command in your terminal:

```
python3 -i util.py
```

Now let’s check what hex does it generate for our string `PEPE Token` run the following command:

```
str_to_felt("PEPE Token")
```

You have the first value, name, ready now i.e.: `379067928035501022078318`.

For `_symbol`, you can generate this using the same `util.py` file by running the `str_to_felt()` function. For PEPE it’d be `1346719813` .

- You should generate your unique token name and symbol and replace their values where required.
    
    

You can run `exit()` when you are done with generating hex. The following image shows output of how I ran my commands:

![Frame 3560364 (22).jpg](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/assests_for_starknet/Writing%20an%20ERC20%20Token/Frame_3560364_(22).webp?raw=true)

## Wrap up

In this lesson, we completed writing ERC20 token in Cairo programming language. In the next lesson, we will deploy it on the Starknet blockchain.