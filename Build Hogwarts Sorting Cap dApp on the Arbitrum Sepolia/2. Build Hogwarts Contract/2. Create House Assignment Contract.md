# Create House Assignment Contract

Welcome back! So we are done with writing our code for Hogwarts contract. Now it’s time to code the house assignment part!! Wohoo, so let’s start coding!

## Create a file

Create a new Solidity file called `RandomHouseAssignment.sol` in the `contracts` directory:

![Frame 3560364 (35).jpg](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Build%20Hogwarts%20Sorting%20Cap%20dApp%20on%20the%20Polygon%20Mumbai/Create%20House%20Assignment%20Contract/Frame_3560364_(35).webp?raw=true>)

## Write code

Let’s write the code

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;
```

- These lines provide SPDX license information and specify the Solidity version to be used (0.8.8).

```
import "./HogwartsNFT.sol";
import {VRFConsumerBaseV2Plus} from "@chainlink/contracts/src/v0.8/vrf/dev/VRFConsumerBaseV2Plus.sol";
import {VRFV2PlusClient} from "@chainlink/contracts/src/v0.8/vrf/dev/libraries/VRFV2PlusClient.sol";
```

- These lines import external contracts and interfaces:
    - `HogwartsNFT.sol`: Imports the `HogwartsNFT` contract, which we wrote in previous lesson.
    - `VRFCoordinatorV2Interface.sol`: Imports an interface from Chainlink VRF (Verifiable Random Function) for version 2 of the VRF coordinator.
    - `VRFConsumerBaseV2Plus.sol`: Imports a contract from Chainlink that helps interact with the Chainlink VRF service.

```
contract RandomHouseAssignment is VRFConsumerBaseV2Plus
```

- This line defines a new Solidity contract named `RandomHouseAssignment`, which inherits from `VRFConsumerBaseV2Plus`. This contract will be responsible for randomly assigning Hogwarts houses to users.

```
  HogwartsNFT public nftContract;
    uint256 private s_subscriptionId; // Changed to uint256 as per VRF 2.5
    bytes32 private i_keyHash; 
    uint32 public callbackGasLimit = 250000;
    mapping(uint256 => address) private s_requestIdToSender;
    mapping(address => string) private s_nameToSender;
```

- These lines declare several state variables for the `RandomHouseAssignment` contract:
    - `nftContract`: An instance of the `HogwartsNFT` contract (we declared in the previous lesson), representing the NFT contract where Hogwarts-themed NFTs are minted.
    - `i_vrfCoordinator`: An interface to the Chainlink VRF coordinator.
    - `i_subscriptionId`: A subscription ID obtained from Chainlink's VRF service.
    - `i_keyHash`: A key hash used for generating random numbers through Chainlink's VRF service.
    - `i_callbackGasLimit`: The gas limit for the callback function called by Chainlink's VRF service.
    - `s_requestIdToSender`: A mapping that associates a request ID with the address of the sender who made the request.
    - `s_nameToSender`: A private mapping that associates sender addresses with names.

```
 event NftRequested(uint256 indexed requestId, address requester);
```

- This line defines an event named `NftRequested`.
- This event will be emitted when a user requests a Hogwarts-themed NFT.
- It includes the `requestId` and `requester` address as indexed parameters for easy retrieval.

```
constructor(
        address _nftContract,
        uint256 subscriptionId,
        bytes32 keyHash,
        uint32 gasLimit
    ) VRFConsumerBaseV2Plus(0x9DdfaCa8183c41ad55329BdeeD9F6A8d53168B1B) { // Replace with actual VRF Coordinator address
        nftContract = HogwartsNFT(_nftContract);
        s_subscriptionId = subscriptionId;
        i_keyHash = keyHash;
        callbackGasLimit = gasLimit;
    }
```

- This is the constructor for the `RandomHouseAssignment` contract. It initializes the contract with various parameters:
    - `_nftContract`: The address of the `HogwartsNFT` contract.
    - `vrfCoordinatorV2Address`: The address of the Chainlink VRF coordinator.
    - `subId`: The subscription ID for Chainlink's VRF service.
    - `keyHash`: The key hash for generating random numbers.
    - `callbackGasLimit`: The gas limit for the callback function.
- In the constructor, the state variables are initialized.
- It also calls the constructor of the parent contract `VRFConsumerBaseV2Plus` with the `vrfCoordinatorV2Address` parameter.

```
function requestNFT(string memory name) public returns (uint256 requestId) {
        // Use the s_vrfCoordinator from VRFConsumerBaseV2Plus and the new request format for VRF 2.5
        requestId = s_vrfCoordinator.requestRandomWords(
            VRFV2PlusClient.RandomWordsRequest({
                keyHash: i_keyHash,
                subId: s_subscriptionId,
                requestConfirmations: 3, // Minimum confirmations
                callbackGasLimit: callbackGasLimit,
                numWords: 1, // Request 1 random word
                extraArgs: VRFV2PlusClient._argsToBytes(
                    VRFV2PlusClient.ExtraArgsV1({nativePayment: false}) // Set nativePayment to false for LINK payment
                )
            })
        );

        s_requestIdToSender[requestId] = msg.sender;
        s_nameToSender[msg.sender] = name;
        emit NftRequested(requestId, msg.sender);
    }
```

- `function requestNFT(string memory name) public {`: This line declares a public function named `requestNFT` that takes a single parameter `name` of type `string` stored in memory. This parameter represents the name associated with the NFT to be minted. It allows a user to request a Hogwarts-themed NFT.
- `uint256 requestId = i_vrfCoordinator.requestRandomWords(`: This line initiates a request to the Chainlink VRF service to generate random words. It assigns the unique `requestId` generated for this request to a local variable named `requestId`. The parameters for this request are as follows:
    - `i_keyHash`: The key hash used to verify randomness.
    - `i_subscriptionId`: The subscription ID used with Chainlink VRF.
    - `3`: The number of random words to request (in this case, 3 words).
    - `i_callbackGasLimit`: The gas limit for the callback function.
    - `1`: A "userProvidedSeed" parameter, which is set to 1 in this case (it can be any value).
- `s_requestIdToSender[requestId] = msg.sender;`: This line records the relationship between the `requestId` and the sender's address (`msg.sender`) in the `s_requestIdToSender` mapping. This mapping is used to associate each request with the address that initiated it.
- `s_nameToSender[msg.sender] = name;`: This line associates the sender's address (`msg.sender`) with the provided `name` parameter in the `s_nameToSender` mapping. This is done to store the name associated with the NFT to be minted.
- `emit NftRequested(requestId, msg.sender);`: This line emits an event named `NftRequested` to log that a request for an NFT has been made. It includes two parameters:
    - `requestId`: The unique identifier for the request.
    - `msg.sender`: The address of the sender who initiated the request.

```
function fulfillRandomWords(uint256 requestId, uint256[] calldata randomWords) internal override {
        address nftOwner = s_requestIdToSender[requestId];
        string memory name = s_nameToSender[nftOwner];
        uint256 house = randomWords[0] % 4;
        nftContract.mintNFT(nftOwner, house, name);
    }
```

- This function, `fulfillRandomWords`, is an internal override that handles the fulfillment of random words generated by Chainlink's VRF service. It does the following:
    - Retrieves the `nftOwner` address from `s_requestIdToSender` using the `requestId`.
    - `string memory name = s_nameToSender[nftOwner];`: Here, the function retrieves the name associated with the NFT owner (the sender's address) from the `s_nameToSender` mapping. This is the name that was provided when making the request.
    - Calculates a random `house` index based on the first random word.
    - `nftContract.mintNFT(nftOwner, house, name);`: Finally, this line calls the `mintNFT` function of the `nftContract` (HogwartsNFT contract) to mint a new NFT. It specifies the following parameters:
        - `nftOwner`: The address of the NFT owner (the requester).
        - `house`: The randomly determined house (0, 1, 2, or 3) for the NFT.
        - `name`: The name associated with the NFT owner, which will be used as part of the NFT's metadata.

## Complete code

The complete code for `RandomHouseAssignment.sol` is:

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;

import "./HogwartsNFT.sol";
import {VRFConsumerBaseV2Plus} from "@chainlink/contracts/src/v0.8/vrf/dev/VRFConsumerBaseV2Plus.sol";
import {VRFV2PlusClient} from "@chainlink/contracts/src/v0.8/vrf/dev/libraries/VRFV2PlusClient.sol";

contract RandomHouseAssignment is VRFConsumerBaseV2Plus {
    HogwartsNFT public nftContract;
    uint256 private s_subscriptionId; // Changed to uint256 as per VRF 2.5
    bytes32 private i_keyHash; 
    uint32 public callbackGasLimit = 250000;
    mapping(uint256 => address) private s_requestIdToSender;
    mapping(address => string) private s_nameToSender;

    event NftRequested(uint256 indexed requestId, address requester);

    constructor(
        address _nftContract,
        uint256 subscriptionId,
        bytes32 keyHash,
        uint32 gasLimit
    ) VRFConsumerBaseV2Plus(0x9DdfaCa8183c41ad55329BdeeD9F6A8d53168B1B) { // Replace with actual VRF Coordinator address
        nftContract = HogwartsNFT(_nftContract);
        s_subscriptionId = subscriptionId;
        i_keyHash = keyHash;
        callbackGasLimit = gasLimit;
    }

    function requestNFT(string memory name) public returns (uint256 requestId) {
        // Use the s_vrfCoordinator from VRFConsumerBaseV2Plus and the new request format for VRF 2.5
        requestId = s_vrfCoordinator.requestRandomWords(
            VRFV2PlusClient.RandomWordsRequest({
                keyHash: i_keyHash,
                subId: s_subscriptionId,
                requestConfirmations: 3, // Minimum confirmations
                callbackGasLimit: callbackGasLimit,
                numWords: 1, // Request 1 random word
                extraArgs: VRFV2PlusClient._argsToBytes(
                    VRFV2PlusClient.ExtraArgsV1({nativePayment: false}) // Set nativePayment to false for LINK payment
                )
            })
        );

        s_requestIdToSender[requestId] = msg.sender;
        s_nameToSender[msg.sender] = name;
        emit NftRequested(requestId, msg.sender);
    }

    function fulfillRandomWords(uint256 requestId, uint256[] calldata randomWords) internal override {
        address nftOwner = s_requestIdToSender[requestId];
        string memory name = s_nameToSender[nftOwner];
        uint256 house = randomWords[0] % 4;
        nftContract.mintNFT(nftOwner, house, name);
    }
}

```

## Wrap up

In this lesson, we learned how to code the house assignment part of the Hogwarts contract. We created a new Solidity file called `RandomHouseAssignment.sol` and wrote the necessary code to randomly assign Hogwarts houses to users. We used Chainlink's VRF service to generate random numbers, and then minted an NFT for the user with the randomly assigned house. With this, we have completed the code for the Hogwarts contract.

In the next lesson, we will finally deploy it, Wohoo, let’s go!

**Assignment Title:** What logic have we used to select houses for the user?

**Assignment:** Let us know.