# Create Hogwarts Contract

Welcome back! So we are done with setting up our project. Now it’s time to code!! Wohoo, so let’s start coding!

## Create a file

Navigate to `contracts` directory. Delete existing `Lock.sol` as we do not need it.

Create a new Solidity file called `HogwartsNFT.sol` in the `contracts` directory:

![Frame 3560364 (34).jpg](<https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Build%20Hogwarts%20Sorting%20Cap%20dApp%20on%20the%20Polygon%20Mumbai/Create%20Hogwarts%20Contract/Frame_3560364_(34).webp?raw=true>)

## Write code

We will write that does the following:

- Allows the owner to mint non-fungible tokens representing the four Hogwarts houses.
- Store NFTs on IPFS and associated them with a specific house.
- Make the tokens soulbound within the Hogwarts context.

So why wait, let’s go!

First are the license and import statements (ignore any red lines or warnings):

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.8;

import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
```

- These lines provide SPDX license information and specify the Solidity version to be used (0.8.8).
- These import statements bring in several pre-built Solidity smart contracts from external libraries:
  - `ERC721URIStorage.sol` is a contract that extends the ERC721 standard by adding support for storing metadata associated with each token, such as an image or description.
  - `Ownable.sol` is a contract that provides a basic access control mechanism, allowing the contract owner to restrict certain functions to themselves.
  - `ERC721.sol` is a contract that provides a basic implementation of ERC721 token.

```
contract HogwartsNFT is ERC721URIStorage, Ownable {
```

- This line defines a new Solidity contract named `HogwartsNFT`.
- It inherits from two other contracts, `ERC721URIStorage` and `Ownable`.
- `ERC721URIStorage` is likely an ERC-721 token standard with URI storage for NFT metadata, and `Ownable` provides ownership functionality.

```
mapping(uint256 => address) public s_requestIdToSender;
mapping(address => uint256) public s_addressToHouse;
mapping(address => bool) public hasMinted;
mapping(address => string) public s_addressToName;

uint256 private s_tokenCounter;
```

These lines declare several state variables:

- `s_requestIdToSender`: A mapping that associates a request ID (represented as a `uint256`) with the address that made that request.
- `s_addressToHouse`: A mapping that associates an address with the index of the Hogwarts house they received.
- `hasMinted`: A mapping that tracks whether an address has already minted an NFT.
- `s_addressToName`: A mapping to associate name with address.
- `s_tokenCounter`: A private variable to keep track of the number of NFTs minted.

```
string[] internal houseTokenURIs = [
        "ipfs://QmXja2QBKsNW9qnw9kKfcm25rJTomwAVJUrXekYFJnVwbg/Gryffindor.json",
        "ipfs://QmXja2QBKsNW9qnw9kKfcm25rJTomwAVJUrXekYFJnVwbg/Hufflepuff.json",
        "ipfs://QmXja2QBKsNW9qnw9kKfcm25rJTomwAVJUrXekYFJnVwbg/Ravenclaw.json",
        "ipfs://QmXja2QBKsNW9qnw9kKfcm25rJTomwAVJUrXekYFJnVwbg/Slytherin.json"
    ];
```

- It declares an internal array called `houseTokenURIs`, which contains strings representing the URIs for the token metadata of the four Hogwarts houses (Gryffindor, Hufflepuff, Ravenclaw, Slytherin). These URIs likely point to JSON files on IPFS.
- You can even upload your own assets on IPFS using Pinata or NFTstorage and replace the IPFS links accordingly.

```
event NftMinted(uint256 house, address minter, string name);
```

- This line defines an event called `NftMinted`, which will be called when an NFT is minted.
- It includes three parameters: the `house` (represented as a `uint256`), `minter` address, and the `name` the user will provide.

```
constructor() ERC721("Hogwarts NFT", "HP") {
    s_tokenCounter = 0;
}
```

- This is the constructor for the contract. It sets the initial state by initializing `s_tokenCounter` to 0 and also calls the constructor of the parent contract `ERC721` with the name "Hogwarts NFT" and symbol "HP."

```
function hasMintedNFT(address _user) public view returns (bool) {
    return hasMinted[_user];
}
```

- This is a public view read only function named `hasMintedNFT`.
- It allows anyone to check whether a specific address `_user` has already minted an NFT.
- It returns a boolean indicating whether the address has minted or not.

```
function getHouseIndex(address _user) public view returns (uint256) {
    return s_addressToHouse[_user];
}
```

- This is another public view function named `getHouseIndex`.
- It allows anyone to query the index of the Hogwarts house associated with a specific address `_user`. It returns a `uint256` representing the index.

```

function mintNFT(address recipient, uint256 house, string memory name) external onlyOwner {
        require(!hasMinted[recipient], "You have already minted your house NFT"); // Ensure the address has not minted before

        uint256 tokenId = s_tokenCounter;
        _safeMint(recipient, tokenId);
        _setTokenURI(tokenId, houseTokenURIs[house]);

        s_addressToHouse[recipient] = house; //map house to address
        s_addressToName[recipient] = name; // map name to address

        s_tokenCounter += 1;
        hasMinted[recipient] = true; // Mark the address as having minted an NFT

        emit NftMinted(house, recipient, name);
    }
```

This is a public function named `mintNFT`, which allows the contract owner (the onlyOwner modifier ensures this) to mint NFTs for users based on their specified recipient address, house index, and name. It performs several actions:

- `require(!hasMinted[recipient], "You have already minted your house NFT");`: This line uses a `require` statement to ensure that the recipient (address) has not already minted an NFT. If the recipient has already minted one, the function will revert with the specified error message.
- `uint256 tokenId = s_tokenCounter;`: This line initializes a variable `tokenId` with the current value of `s_tokenCounter`. This variable will represent the unique identifier for the newly minted NFT.
- `_safeMint(recipient, tokenId);`: This line calls the `_safeMint` function inherited from the ERC721 contract to mint a new NFT for the `recipient` with the specified `tokenId`.
- `_setTokenURI(tokenId, houseTokenURIs[house]);`: This line calls the `_setTokenURI` function to associate the metadata URI for the newly minted NFT with the specified `tokenId` and `house`. The metadata URI is fetched from the `houseTokenURIs` array based on the provided `house` index.
- `s_addressToHouse[recipient] = house;`: This line updates the `s_addressToHouse` mapping, associating the `recipient`'s address with the `house` index. This information indicates which house the NFT represents.
- `s_addressToName[recipient] = name;`: This line updates the `s_addressToName` mapping, associating the `recipient`'s address with the provided `name`. This allows associating a name with the NFT owner's address.
- `s_tokenCounter += 1;`: This line increments the `s_tokenCounter` by 1, ensuring that the next minted NFT will have a unique token ID.
- `hasMinted[recipient] = true;`: This line marks the `recipient`'s address as having minted an NFT by setting the corresponding value in the `hasMinted` mapping to `true`.
- `emit NftMinted(house, recipient, name);`: Finally, this line emits the `NftMinted` event with information about the `house`, `recipient` address, and `name` of the minted NFT. This event allows external parties to listen for NFT minting events and react accordingly.

```
function _beforeTokenTransfer(address from, address to, uint256 firstTokenId, uint256 batchSize) internal virtual override {
        super._beforeTokenTransfer(from, to, firstTokenId, batchSize);

        require(from == address(0) || to == address(0), "Err! This is not allowed in Hogwarts");
    }
}
```

- This function overrides the `_beforeTokenTransfer` function of the parent contract `ERC721`.
- It enforces a custom transfer condition where transfers are only allowed from or to the zero address (`address(0)`), indicating that the tokens are **soulbound** within the Hogwarts context.
- If this condition is not met, it reverts the transaction.

## Complete code

The complete code for the `HogwartsNFT.sol` is:

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.8;

import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";

contract HogwartsNFT is ERC721URIStorage, Ownable {
    mapping(uint256 => address) public s_requestIdToSender;
    mapping(address => uint256) public s_addressToHouse;
    mapping(address => bool) public hasMinted;
    mapping(address => string) public s_addressToName;

    uint256 private s_tokenCounter;

    string[] internal houseTokenURIs = [
        "ipfs://QmXja2QBKsNW9qnw9kKfcm25rJTomwAVJUrXekYFJnVwbg/Gryffindor.json",
        "ipfs://QmXja2QBKsNW9qnw9kKfcm25rJTomwAVJUrXekYFJnVwbg/Hufflepuff.json",
        "ipfs://QmXja2QBKsNW9qnw9kKfcm25rJTomwAVJUrXekYFJnVwbg/Ravenclaw.json",
        "ipfs://QmXja2QBKsNW9qnw9kKfcm25rJTomwAVJUrXekYFJnVwbg/Slytherin.json"
    ];

    event NftMinted(uint256 house, address minter, string name);

    constructor() ERC721("Hogwarts NFT", "HP") {
        s_tokenCounter = 0;
    }

    // Read-only function to check whether a user has minted an NFT
    function hasMintedNFT(address _user) public view returns (bool) {
        return hasMinted[_user];
    }

    // Read-only function to get the house index mapped to the user
    function getHouseIndex(address _user) public view returns (uint256) {
        return s_addressToHouse[_user];
    }

    //Function to mint NFT according to the house index
    function mintNFT(address recipient, uint256 house, string memory name) external onlyOwner {
        require(!hasMinted[recipient], "You have already minted your house NFT");

        uint256 tokenId = s_tokenCounter;
        _safeMint(recipient, tokenId);
        _setTokenURI(tokenId, houseTokenURIs[house]);

        s_addressToHouse[recipient] = house;
        s_addressToName[recipient] = name;

        s_tokenCounter += 1;
        hasMinted[recipient] = true;

        emit NftMinted(house, recipient, name);
    }


    function _beforeTokenTransfer(address from, address to, uint256 firstTokenId, uint256 batchSize) internal virtual override {
        super._beforeTokenTransfer(from, to, firstTokenId, batchSize);

        require(from == address(0) || to == address(0), "Err! This is not allowed in Hogwarts");
    }

}
```

## Wrap up

In this lesson, we learned how to create a new Solidity contract, import existing Solidity smart contracts, and use OpenZeppelin and Chainlink libraries. We created a contract called `HogwartsNFT.sol` that allows the owner to mint non-fungible tokens representing the four Hogwarts houses. The NFTs are stored on IPFS and are associated with a specific house. The contract also enforces a custom transfer condition where transfers are only allowed from or to the zero address, indicating that the tokens are soulbound within the Hogwarts context.

In the next lesson, we will write the logic of our application. Stay tuned!
