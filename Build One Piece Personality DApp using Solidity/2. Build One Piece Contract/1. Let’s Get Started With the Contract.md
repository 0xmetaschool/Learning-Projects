# Let’s Get Started With the Contract

Oi, oi, oi, welcome back! We are done with setting up our project. Now, it’s time to code!! Woohoo, let’s start coding!

## Create a file

Navigate to the `contracts` directory. Delete the existing `Lock.sol` file as we do not need it. 

Create a new Solidity file called `OnePieceMint.sol` in the `contracts` directory.

## Write code

We will write the `OnePieceMint` smart contract that does the following:

- Allows the owner to mint non-fungible tokens representing the five One Piece characters.
- Randomly assign a character to a user based on the questions asked via frontend.
- Create a string of IPFS links of character NFTs.
- Make the tokens soul-bound within the One Piece context.

So why wait, let’s go!

### Import dependencies

We will start our Solidity file by importing the dependencies like any other smart contract.

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.8;

import "@chainlink/contracts/src/v0.8/interfaces/VRFCoordinatorV2Interface.sol";
import "@chainlink/contracts/src/v0.8/vrf/VRFConsumerBaseV2.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
```

- First of all, we are importing chainlink smart contracts. We need these smart contracts for randomness. VRF chainlink is a random number generator that enables smart contracts to access random values. This generator will help us assign characters to users randomly.
- Openzeppelin smart contracts are used to create an ERC721 non-fungible token contract.

### Create a smart contract

Create the `OnePieceMint` smart contract that inherits all the contracts we have imported.

```
contract OnePieceMint is VRFConsumerBaseV2, ERC721, Ownable, ERC721URIStorage {
	 // Contract code goes here
}
```

From now on, we will add the code to the `OnePieceMint` contract.

### Add character NFTs URIs

For your ease, we have already created the metadata files and uploaded them to Pinata to create the IPFS files. You can create your NFTs by following the following steps:

1. Create an account on Pinata.
2. Upload image files to Pinata.
3. Create metadata JSON files using Pinata image links.
4. Upload metadata files to Pinata.
5. Use metadata file links in your code.

Here’s how we have created the URIs string for five One Piece characters.

```
string[] internal characterTokenURIs = [
		"https://scarlet-live-iguana-759.mypinata.cloud/ipfs/QmNp4sHf4ccqPpqMBUCSG1CpFwFR4D6kgHesxc1mLs75am",
		"https://scarlet-live-iguana-759.mypinata.cloud/ipfs/QmPHaFt55PeidgCuXe2kaeRYmLaBUPE1Y7Kg4tDyzapZHy",
		"https://scarlet-live-iguana-759.mypinata.cloud/ipfs/QmP9pC9JuUpKcnjUk8GBXEWVTGvK3FTjXL91Q3MJ2rhA16",
		"https://scarlet-live-iguana-759.mypinata.cloud/ipfs/QmSnNXo5hxrFnpbyBeb7jY7jhkm5eyknaCXtr8muk31AHK",
		"https://scarlet-live-iguana-759.mypinata.cloud/ipfs/QmarkkgDuBUcnqksatPzU8uNS4o6LTbEtuK43P7Jyth9NH"
];
```

### Add local variables

As always, we will create multiple local variables that will help us store important data in the smart contract.

```
uint256 private s_tokenCounter; // Used to keep track of the number of NFTs being minted
VRFCoordinatorV2Interface private i_vrfCoordinator; // Used to store VRF coordinator link
uint64 private i_subscriptionId; // Used to store subscription ID from VRF chainlink
bytes32 private i_keyHash; // Used to store key hash from VRF chainlink
uint32 private i_callbackGasLimit; // Used to specify the gas limit
```

### Add mappings

We will declare several mappings to store relationships between `requestId` and sender, user and their chosen character, address and whether they have minted, and address and the character they got.

```
mapping(uint256 => address) private requestIdToSender; // allows the contract to keep track of which address made a request
mapping(address => uint256) private userCharacter; // enables the contract to associate each user with their selected character
mapping(address => bool) public hasMinted; // prevents users from minting multiple NFTs with the same address
mapping(address => uint256) public s_addressToCharacter; // allows users to query which character they received based on their address
```

### Add events

We will declare events to log NFT requests, character traits determined, and NFTs minted.

```
event NftRequested(uint256 requestId, address requester);
event CharacterTraitDetermined(uint256 characterId);
event NftMinted(uint256 characterId, address minter);
```

### Add a constructor function

Creating a constructor for each contract is essential. In our case, we will create a constructor as follows.

```
constructor(
    address vrfCoordinatorV2Address,
    uint64 subId,
    bytes32 keyHash,
    uint32 callbackGasLimit
) VRFConsumerBaseV2(vrfCoordinatorV2Address) ERC721("OnePiece NFT", "OPN"){

    i_vrfCoordinator = VRFCoordinatorV2Interface(vrfCoordinatorV2Address);
    i_subscriptionId = subId;
    i_keyHash = keyHash;
    i_callbackGasLimit = callbackGasLimit;
}
```

The constructor function takes VRF chainlink data and initializes the contract variables.

### Creating the minting function

As we’re done with all the essentials, let’s come to real business and create the minting function that will officially mint the character NFT.

```
function mintNFT(address recipient, uint256 characterId) internal {
    // Ensure the address has not been minted before
    require(!hasMinted[recipient], "You have already minted your house NFT");

    // Get the next available token ID
    uint256 tokenId = s_tokenCounter;

    // Mint the NFT and assign it to the recipient
    _safeMint(recipient, tokenId);

    // Set the token URI for the minted NFT based on the character ID
    _setTokenURI(tokenId, characterTokenURIs[characterId]);

    // Map the recipient's address to the character ID they received
    s_addressToCharacter[recipient] = characterId;

    // Increment the token counter for the next minting
    s_tokenCounter += 1;

    // Mark the recipient's address as having minted an NFT
    hasMinted[recipient] = true;

    // Emit an event to log the minting of the NFT
    emit NftMinted(characterId, recipient);
}
```

### Creating the requestNFT function

The `requestNFT` function determines the character based on the answers, requests random words from the VRF coordinator, and emits an event.

```
function requestNFT(uint256[5] memory answers) public {
    // Determine the character based on the provided answers and store it for the user
    userCharacter[msg.sender] = determineCharacter(answers);

    // Request random words from the VRF coordinator to determine the character traits
    uint256 requestId = i_vrfCoordinator.requestRandomWords(
        i_keyHash, 
        i_subscriptionId,
        3,
        i_callbackGasLimit,
        1
    );

    // Map the request ID to the sender's address for later reference
    requestIdToSender[requestId] = msg.sender;

    // Emit an event to log the request for the NFT
    emit NftRequested(requestId, msg.sender);
}
```

## Complete code so far

Let’s compile the code we have written so far and get a bird’s eye view.

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.8;

import "@chainlink/contracts/src/v0.8/interfaces/VRFCoordinatorV2Interface.sol";
import "@chainlink/contracts/src/v0.8/vrf/VRFConsumerBaseV2.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";

contract OnePieceMint is VRFConsumerBaseV2, ERC721, Ownable, ERC721URIStorage {
		 string[] internal characterTokenURIs = [
				"https://scarlet-live-iguana-759.mypinata.cloud/ipfs/QmNp4sHf4ccqPpqMBUCSG1CpFwFR4D6kgHesxc1mLs75am",
				"https://scarlet-live-iguana-759.mypinata.cloud/ipfs/QmPHaFt55PeidgCuXe2kaeRYmLaBUPE1Y7Kg4tDyzapZHy",
				"https://scarlet-live-iguana-759.mypinata.cloud/ipfs/QmP9pC9JuUpKcnjUk8GBXEWVTGvK3FTjXL91Q3MJ2rhA16",
				"https://scarlet-live-iguana-759.mypinata.cloud/ipfs/QmSnNXo5hxrFnpbyBeb7jY7jhkm5eyknaCXtr8muk31AHK",
				"https://scarlet-live-iguana-759.mypinata.cloud/ipfs/QmarkkgDuBUcnqksatPzU8uNS4o6LTbEtuK43P7Jyth9NH"
		];

		uint256 private s_tokenCounter; // Used to keep track of the number of NFTs being minted
		VRFCoordinatorV2Interface private i_vrfCoordinator; // Used to store VRF coordinator link
		uint64 private i_subscriptionId; // Used to store subscription ID from VRF chainlink
		bytes32 private i_keyHash; // Used to store key hash from VRF chainlink
		uint32 private i_callbackGasLimit; // Used to specify the gas limit
		
		mapping(uint256 => address) private requestIdToSender; // allows the contract to keep track of which address made a request
		mapping(address => uint256) private userCharacter; // enables the contract to associate each user with their selected character
		mapping(address => bool) public hasMinted; // prevents users from minting multiple NFTs with the same address
		mapping(address => uint256) public s_addressToCharacter; // allows users to query which character they received based on their address

		event NftRequested(uint256 requestId, address requester);
		event CharacterTraitDetermined(uint256 characterId);
		event NftMinted(uint256 characterId, address minter);

		constructor(
		    address vrfCoordinatorV2Address,
		    uint64 subId,
		    bytes32 keyHash,
		    uint32 callbackGasLimit
		) VRFConsumerBaseV2(vrfCoordinatorV2Address) ERC721("OnePiece NFT", "OPN"){
		
		    i_vrfCoordinator = VRFCoordinatorV2Interface(vrfCoordinatorV2Address);
		    i_subscriptionId = subId;
		    i_keyHash = keyHash;
		    i_callbackGasLimit = callbackGasLimit;
		}

		// Function to mint NFT according to the character id
		function mintNFT(address recipient, uint256 characterId) internal {
		    // Ensure the address has not been minted before
		    require(!hasMinted[recipient], "You have already minted your house NFT");
		
		    // Get the next available token ID
		    uint256 tokenId = s_tokenCounter;
		
		    // Mint the NFT and assign it to the recipient
		    _safeMint(recipient, tokenId);
		
		    // Set the token URI for the minted NFT based on the character ID
		    _setTokenURI(tokenId, characterTokenURIs[characterId]);
		
		    // Map the recipient's address to the character ID they received
		    s_addressToCharacter[recipient] = characterId;
		
		    // Increment the token counter for the next minting
		    s_tokenCounter += 1;
		
		    // Mark the recipient's address as having minted an NFT
		    hasMinted[recipient] = true;
		
		    // Emit an event to log the minting of the NFT
		    emit NftMinted(characterId, recipient);
		}

		// Function to request NFT for specific answers
		function requestNFT(uint256[5] memory answers) public {
		    // Determine the character based on the provided answers and store it for the user
		    userCharacter[msg.sender] = determineCharacter(answers);
		
		    // Request random words from the VRF coordinator to determine the character traits
		    uint256 requestId = i_vrfCoordinator.requestRandomWords(
		        i_keyHash, 
		        i_subscriptionId,
		        3,
		        i_callbackGasLimit,
		        1
		    );
		
		    // Map the request ID to the sender's address for later reference
		    requestIdToSender[requestId] = msg.sender;
		
		    // Emit an event to log the request for the NFT
		    emit NftRequested(requestId, msg.sender);
		}
}
```

## That’s a wrap

So far, we have implemented the minting and requesting NFT functionality in our `OnePieceMint` contract. Next, we will implement the randomness functionalities that will help us assign random characters to a specific address. Also, we will add some other essential functionalities.