# Game State Management

Welcome! You did great in writing and understanding the basic contract. Now it’s time to understand an important concept of randomness on which the major logic of our game depends. Let’s explore it!

## Understanding Aptos roll: The unpredictable lottery for dApps

Aptos Roll provides a secure way to generate random numbers on the Aptos blockchain. This is crucial for decentralized applications (dApps) that rely on randomness, such as lotteries, games, and other applications needing unbiased random outcomes.

![aptos-c4-l7 (1).gif](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/C4%20Rock%20Paper%20Scissor%20on%20Aptos%20Images/Lesson%208%20Game%20State%20Management/aptos-c4-l7_(1).webp?raw=true)

> **The code and information have been referred from Aptos’ official [documentation](https://aptos.dev/en/build/smart-contracts/randomness) on Aptos roll.**
> 

### What it does: A quick example

In a centralized system, picking a random winner from a list of participants is straightforward. For instance, using Python, you could simply use `random.randint(0, n-1)` to get a random index. However, achieving the same in a decentralized system like Aptos without a trusted server is challenging.

### Insecure methods of generating random numbers

Before the introduction of Aptos Roll, developers often resorted to insecure methods for generating random numbers. For example:

```
module module_owner::lottery {
    struct LotteryState {
        players: vector<address>,
        winner_idx: std::option::Option<u64>,
    }

    fun load_lottery_state_mut(): &mut LotteryState {
        // ...
    }

    entry fun decide_winner() {
        let lottery_state = load_lottery_state_mut();
        let n = std::vector::length(&lottery_state.players);
        let winner_idx = aptos_framework::timestamp::now_microseconds() % n;
        lottery_state.winner_idx = std::option::some(winner_idx);
    }
}
```

This approach is insecure for several reasons:

1. **Transaction timing**: A malicious user can influence the result by choosing the transaction submission time.
2. **Validator influence**: A malicious validator can bias the outcome by selecting which block includes the `decide_winner` transaction.

### Secure but complex external randomness

Some developers turned to external randomness sources like drand, but this introduces complexity:

1. Participants agree to use a future randomness seed.
2. Once the seed is revealed, clients fetch it and derive the winner locally.
3. One participant submits the seed and the winner is on-chain.

## Achieving simplicity and security with Aptos roll

With Aptos Roll, generating secure random numbers is straightforward. Here's how you can use it effectively:

### 1. Making randomness-dependent entry functions compliant

Annotate entry functions that depend on randomness with `#[randomness]` to make them compliant:

```
#[randomness]
entry fun decide_winner() {
    let lottery_state = load_lottery_state_mut();
    let n = vector::length(&lottery_state.players);
    let winner_idx = aptos_framework::randomness::u64_range(0, n);
    lottery_state.winner_idx = std::option::some(winner_idx);
}
```

This attribute ensures that the function is recognized by the Aptos VM and can safely use the randomness API.

### 2. Calling the API

Use the randomness functions provided by the API. Here are some examples:

```
module aptos_framework::randomness {
    // Generates a number uniformly at random.
    fun u8_integer(): u8 {}
    fun u16_integer(): u16 {}
    // Generates a number `[min_incl, max_excl)` uniformly at random.
    fun u8_range(min_incl: u8, max_excl: u8): u8 {}
    fun u16_range(min_incl: u16, max_excl: u16): u16 {}
    // Generates a sequence of bytes uniformly at random.
    fun bytes(n: u64): vector<u8> {}
    // Generate a permutation of `[0, 1, ..., n-1]` uniformly at random.
    fun permutation(n: u64): vector<u64> {}
}
```

These functions allow you to generate random numbers and sequences, which can be used in various dApp scenarios.

### 3. Security considerations

While the randomness API is secure, developers must be aware of potential vulnerabilities like undergasing attacks. Here are some tips:

- Ensure the gas cost of your functions is independent of the randomness outcome.
- Only allow trusted admins to execute randomness transactions if applicable.
- Make the path that is most beneficial have the highest gas cost to prevent attackers from biasing the outcome.

## How to use Aptos randomness API
Let's find out how to use the Aptos randomness API in your code.

### 1. Supported networks

- The API is enabled on devnet and testnet, with mainnet support coming soon.

### 2. Preventing undergasing attacks

- Ensure the gas cost of your functions is independent of the randomness outcome.
- Only trusted admins should execute randomness transactions if applicable.

### Example API usage

A complete list of API functions for generating random numbers of various types is available. Here’s an overview:

```
module aptos_framework::randomness {
    fun u8_integer(): u8 {}
    fun u16_integer(): u16 {}
    fun u8_range(min_incl: u8, max_excl: u8): u8 {}
    fun u16_range(min_incl: u16, max_excl: u16): u16 {}
    fun bytes(n: u64): vector<u8> {}
    fun permutation(n: u64): vector<u64> {}
}
```

Aptos Roll simplifies the generation of secure random numbers for dApps on the Aptos blockchain, providing a necessary tool for developers to create fair and secure applications. However, developers must remain vigilant against potential attacks and design their contracts accordingly.

We will use this feature to implement the logic of our game. So let’s begin~ 

## Quick checkout

First, let’s quickly swap out our boilerplate with the corresponding front end for these functionalities. 

```
git checkout origin/boilerplate_03
```

Don't worry, all the code we’ve written so far will be present in the project. Navigate to the `sources/RockPaperScissors.move` file. The complete code of `RockPaperScissors.move` file is [here](https://github.com/0xmetaschool/rock-paper-scissors-aptos-dapp/blob/boilerplate_03/sources/RockPaperScissors.move).

We will dive into the features and functionalities that we have added.

## Code explanation

First, we will add the `randomness` module to our code.

```
use aptos_framework::randomness;
```

Next, we will define the `duel` function.

```
#[randomness]
entry fun duel(account: &signer, user_selection: String) acquires DuelResult {
```

- `#[randomness]` is an attribute that indicates this function will use randomness.
- `entry fun duel` defines a public entry function named `duel` that can be called externally.
- The function takes two parameters:
    - `account: &signer`: A reference to the signer's account.
    - `user_selection: String`: A string representing the user's selection (Rock, Paper, or Scissors).
- `acquires DuelResult` specifies that this function will access and modify `DuelResult` resources in the global storage.

```
let random_number = randomness::u64_range(0, 3); // 3 is exclusive
```

- This line generates a random number between 0 and 2 (inclusive) using the `randomness::u64_range` function. The `3` is exclusive, meaning the possible values are 0, 1, or 2.

```
let result = borrow_global_mut<DuelResult>(signer::address_of(account));
```

- This line borrows the `DuelResult` resource associated with the account's address from the global storage, allowing it to be modified.

```
if(random_number==0)
{
    result.computer_selection = utf8(b"Rock");
}
else
{
    if(random_number==1)
    {
        result.computer_selection = utf8(b"Paper");
    }
    else
    {
        result.computer_selection = utf8(b"Scissors");
    }
};
```

- This block sets the `computer_selection` field of the `DuelResult` resource based on the generated random number:
    - If `random_number` is 0, the computer's selection is "Rock".
    - If `random_number` is 1, the computer's selection is "Paper".
    - If `random_number` is 2, the computer's selection is "Scissors".

```
let computer_selection = &result.computer_selection;
```

- This line creates a reference to the `computer_selection` field of the `DuelResult` resource.

```
if (user_selection == *computer_selection) {
    result.duel_result = utf8(b"Draw"); // Draw
} else if ((user_selection == utf8(b"Rock") && *computer_selection == utf8(b"Scissors")) ||
           (user_selection == utf8(b"Paper") && *computer_selection == utf8(b"Rock")) ||
           (user_selection == utf8(b"Scissors") && *computer_selection == utf8(b"Paper"))) {
    result.duel_result = utf8(b"Win"); // User wins
} else {
    result.duel_result = utf8(b"Lose"); // Computer wins
}
```

- This block determines the result of the duel based on the user's selection and the computer's selection:
    - If the user's selection matches the computer's selection, the result is a draw.
    - If the user's selection beats the computer's selection (according to Rock-Paper-Scissors rules), the result is a win for the user.
    - Otherwise, the result is a win for the computer.
- The `duel_result` field of the `DuelResult` resource is updated accordingly:
    - "Draw" if it's a tie.
    - "Win" if the user wins.
    - "Lose" if the computer wins.

Lastly, we have a `get_result` function that retrieves the computer's selection and the duel result for a given user's account. It takes the signer's account as a parameter, borrows the `DuelResult` resource associated with the account's address, and returns a tuple containing the computer's selection and the duel result.

```
    public fun get_result(account: &signer): (String, String) acquires DuelResult {
        let result = borrow_global<DuelResult>(signer::address_of(account));
        (result.computer_selection, result.duel_result)
    }
```

Whew! We are doneeeeeee with the code! Let’s deploy and see how it works.

## Deploy your dApp

Let’s deploy the contract using the CLI. Open a terminal in the `rock-paper-scissors-aptos-dapp` folder and run the following command:

```
aptos move compile
```

The command will essentially compile our code and if the compilation is successful that means our code is mostly error-free and ready to be pushed to a network. The output will look like this (ignore the warnings):

![aptos-c4-l7.jpg](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/C4%20Rock%20Paper%20Scissor%20on%20Aptos%20Images/Lesson%208%20Game%20State%20Management/aptos-c4-l7.webp?raw=true)

## Publish the contract

The following command will publish our code to the Aptos testnet. While running the command, it will prompt you regarding the transaction fee payment, just add a ‘yes’ there.

```
aptos move publish
```

And once that is done, the CLI will automatically publish the code to the Aptos testnet. The output will look like this (ignore the warnings):

![aptos-c4-l7 (2).jpg](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/C4%20Rock%20Paper%20Scissor%20on%20Aptos%20Images/Lesson%208%20Game%20State%20Management/aptos-c4-l7_(2).webp?raw=true)

## That’s a wrap

Woohoo! You did great! We implemented the complete logic of our game and deployed it. In the next lesson, we will complete the frontend and interact with it. Stay tuned!