
# Letâ€™s work on the smart contract

##### âœðŸ¼ Writing your smart contract

### So what is the most important part of any Ethereum DApp?

If you shouted smart contracts then you are absolutely right!

Creating the smart contract will give us a concrete base for the rest of the dApp, we will capture all of the functionality of our project in one simple smart contract, and then we will build the rest of the application around it!

Now if you have developed any smart contract before then there is a high probability that you have used either the Remix IDE or used VSCode with some 3rd party library like Hardhat or Foundry.

To make it easier for our learners, we are going to use the [Remix IDE](https://remix.ethereum.org/), but if you are a fan of VS Code Solidity development then please feel free to use that as well.

So first things first, create a new file called **TwitterDapp.sol**

When you log into the Remix IDE you should see something like this

![](https://lh4.googleusercontent.com/U-c9lOEvgPBCw6yedqb-UXbaJmR-xktvdLtE-2SoonHovdb78aq9oVK3BaDvazfGybo1voWx81ZIf8VJ47f4HkhuVFGCVnLC0yuPSxM4ky0b-TpIbDUQgK8lD4FsbdbnX4w4sfuGz1B1-MaDEDBGdsAkVMLoUtuto485BA1DGi2n5BpdNB2tD4dYOw)

Right click on the contracts folder and create a new file called â€œTwitterDapp.solâ€

![](https://lh4.googleusercontent.com/PgVmV7R8bki2R1sLXexj1zpJXWNWOxDjkfevBBUO4l8bbMFJJfyOh5OO6_VxFzGOif3Xl-o7zc9-YJJswlaxTds4TBMpMMZrttb8yJIh4LyviWHmW0eZqCTBa8Tg7YQ2bcCDn1w-mGogH62ohcqsb64jnliT-_1bX9WLC0PGKp4XTlwIPSLNAaYPpQ)

  
**Now create a skeleton smart contract by adding the following code.**

```awk
// SPDX-License-Identifier: UNLICENSED
pragma solidity >=0.8.0;

contract TwitterDapp {}
```

**Adding import statements**

Just above the contract definition add the following import statements

```d
// SPDX-License-Identifier: UNLICENSED
pragma solidity >=0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/utils/Strings.sol";
import "@openzeppelin/contracts/utils/Base64.sol";

contract TwitterDapp is ERC721("TwitterDApp", "TDAPP") {}
```

The first line of the contract refers to the License Identifier. Since our contract does not refer to any license, we declare it as UNLICENSED

```awk
// SPDX-License-Identifier: UNLICENSED
```

-   The second line of the contract should define the Solidity compiler version. Our contract requires a minimum version of _0.8.0_  and above, and therefore referred as:

```apache
pragma solidity >=0.8.0;
```

-   We now need to import any external contracts and/or libraries required for our project. In our case, we are using OpenZeppelinâ€™s _ERC721_  to start off with a standard ready-made structure for ERC721. We also need to import OpenZeppelinâ€™s _Strings_  and _Base64_  libraries. For our NFTs to be vieweable on OpenSea, we require a means to resolve our _tokenURI_  function to return a JSON for our tweetâ€™s metadata. _Strings_  will allow us string manipulation (which does not come built-in Solidity) and _Base64_  will allow us to format our result to JSON. We add the following lines to import:

```nginx
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/utils/Strings.sol";
import "@openzeppelin/contracts/utils/Base64.sol";
```

Now that the headers are set up, we move on to defining our contract. Our contract will be called twitterDapp and will inherit from Open Zeppelin's ERC721, to be defined with name as _TwitterDApp_  and symbol as _TDAPP_:

```csharp
contract TwitterDapp is ERC721("TwitterDApp", "TDAPP"){

}
```

**Creating the state variables and structs**

Before writing functions for a smart contract, it is always a good idea to define all of the global entities that you might need.

So letâ€™s do exactly that.

Add the following code inside your smart contract

```csharp
contract TwitterDapp is ERC721("TwitterDApp", "TDAPP") {
    uint256 tokenId;
    tweet[] public tweets;

    struct tweet{
        string name;
        string description;
        uint256 upvotes;
        string[] comments;
        address fromAddress;
    }
}
```

The very first thing we need is a way to store the metadata of the tweets on our platform, namely, the name (author) of the tweet, its description (the tweet itself), its upvotes, its comments and address of the tweeter. To cater to this requirement, we go ahead and create a _struct_  in our contract:

```abnf
struct tweet{
        string name;
        string description;
        uint256 upvotes;
        string[] comments;
        address fromAddress;
}
```

We now need a means to assign a unique _tokenId_  to refer to each distinct tweet. For this purpose, we declare a global variable of integer type as follows:

```abnf
uint256 tokenId;
```

And, in order to store all tweets, we declare a dynamic length array of type _tweet_:

```abnf
tweet[] public tweets;
```

Whenever a tweet is _minted_  or its _metadata_  updated, we will reference the tweet in our array to update the respective details. Note that our _tokenId_  counter start from _0_  and therefore, passing in the _tokenId_  as an index to our array will help locate the respective tweet.

**The Functions**

We now move over to defining the functionality (functions) of our smart contract. Think of all the actions a user is permitted to take.

**tokenURI Function**

At the very core, we need a mechanism for the _tokenURI_  (a standard function to return metadata of NFTs in JSON) to be accessed and resolved by OpenSea.

We, therefore, define a function called _tokenURI_  that takes as argument a _uint256_  integer (for reference to our tokenId). Next, in reference to [OpenSea standards](https://docs.opensea.io/docs/metadata-standards)  we need to output _name_, _description_  and _attributes_  (_Upvotes_  in our case).

We utilize the _Strings_  library to format our output as per JSON standards and then the _Base64_  library to resolve it to JSON as follows:

```scilab
function tokenURI(uint256 _tokenId)
        public
        view
        override
        returns (string memory)
    {
        bytes memory dataURI = abi.encodePacked(
            '{',
                '"name":', '"', tweets[_tokenId].name, '",'  '"description":' , '"',  tweets[_tokenId].description, '"', ',' ,
            
            '"attributes":', '[', '{', '"trait_type":', '"Upvotes",' , '"value":', Strings.toString(tweets[_tokenId].upvotes), '}', ']' , '}'
        );


        return string(
            abi.encodePacked(
                "data:application/json;base64,",
                Base64.encode(dataURI)
            )
        );
    }
```

**writeTweet Function**

We now move on to the _writeTweet_  function for users to be able to _mint_  new tweets. We take as argument _name_  and _description_  of the tweet.

We utilize the __safeMint_  function offered by OpenZeppelinâ€™s ERC721 which we imported for minting purposes.

We then initialize our _tweet_  struct with empty values and push to our array of tweets.

We also set the _fromAddress_  of the tweeter using _msg.sender_. The global _tokenId_  is then incremented for keeping unique token ids for future minting:

```reasonml
function writeTweet(string memory prefName, string memory prefDesc) public {
        _safeMint(msg.sender, tokenId);
        tweets.push(tweet({
            name: prefName,
            description: prefDesc,
            upvotes: 0,
            comments: new string[](0),
            fromAddress: msg.sender
        }));
        tokenId = tokenId + 1;
    }
```

**addComment Function**

Similarly, we add a function for the user to add comments for a tweet. We take as argument the _tokenId_  as index of the _tweets_  array and the comment they want to add for it and push to the comments array of the respective tweet:

```reasonml
function addComment(uint256 tweetIndex, string memory prefComments) public {
    tweets[tweetIndex].comments.push(prefComments);
}
```

**upVote Function**

The _Upvote_  function follows the same strategy as _addComment_:

```csharp
   function upvote(uint tweetIndex) public {
        tweets[tweetIndex].upvotes += 1;
    }
```

**getAllTweets Function**

Lastly, for the frontend to display all the tweets with their metadata, we require a function to get a list of all tweets. We write a _getAllTweets()_  function without arguments that returns our _tweets_  array:

```ada
   function getAllTweets() public view returns(tweet[] memory) {
        return tweets;
    }
```

You did it! You finally did it! You should be proud of making it this far. ðŸ¥¹

![success](https://lh6.googleusercontent.com/kUQy0fk3tA-PnXjskKtDyERE0EhoH0p6OiHGjpq0gXAgMwXTOsRT4ZRXXa00rQ2IcFxoeiJlVawxh8dR6IOQ3yG4BuRmSsHrOMXd4B-GxG0swO3CreBLPAFrqN4yteIsvIGN4O95z9ITAcwRHUl43hxUXvlmvQ4v6b8s8LkgTyMQ2DRBbUQBHXySDQ)

**Our smart contract is now ready. With everything combined, the smart contract should now look something like this!**

```php
// SPDX-License-Identifier: UNLICENSED
pragma solidity >=0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/utils/Strings.sol";
import "@openzeppelin/contracts/utils/Base64.sol";


contract TwitterDapp is ERC721("TwitterDApp", "TDAPP") {
    uint256 tokenId;
    tweet[] public tweets;

    struct tweet{
        string name;
        string description;
        uint256 upvotes;
        string[] comments;
        address fromAddress;
    }

    function tokenURI(uint256 _tokenId)
        public
        view
        override
        returns (string memory)
    {
        bytes memory dataURI = abi.encodePacked(
            '{',
                '"name":', '"', tweets[_tokenId].name, '",'  '"description":' , '"',  tweets[_tokenId].description, '"', ',' ,
            
            '"attributes":', '[', '{', '"trait_type":', '"Upvotes",' , '"value":', Strings.toString(tweets[_tokenId].upvotes), '}', ']' , '}'
        );

        return string(
            abi.encodePacked(
                "data:application/json;base64,",
                Base64.encode(dataURI)
            )
        );
    }

    function writeTweet(string memory prefName, string memory prefDesc) public {
        _safeMint(msg.sender, tokenId);
        tweets.push(tweet({
            name: prefName,
            description: prefDesc,
            upvotes: 0,
            comments: new string[](0),
            fromAddress: msg.sender
        }));
        tokenId = tokenId + 1;
    }

    function upvote(uint tweetIndex) public {
        tweets[tweetIndex].upvotes += 1;
    }

    function addComment(uint256 tweetIndex, string memory prefComments) public {
        tweets[tweetIndex].comments.push(prefComments);
    }

    function getAllTweets() public view returns(tweet[] memory) {
        return tweets;
    }
}
```

And that's all for this lesson!

Onto the next one. ðŸ¤‘