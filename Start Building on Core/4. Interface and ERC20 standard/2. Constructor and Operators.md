# Constructor and Operators

Hey, welcome! So excited to see you back. In the last lesson, we laid the groundwork for our Core lending dApp. Now in this lesson, we will dive into two important concepts in Solidity smart contract development: Constructors and Operators. We'll also explore how to use these concepts in practice while building a simple deposit and withdrawal functionality. So why wait, let’s just dive into it right away.

## Quick Checkout

First, let’s quickly swap out our boilerplate with the corresponding frontend for these functionalities.

```solidity
git checkout origin/boilerplate02
```

Don't worry, all the code we’ve written so far will be present in the project. Navigate to the `CoreLoanPlatform.sol` file present inside the `contracts` folder and let’s add the required functions there.

Before we dive into constructors, let's review the changes made to our boilerplate code:

- **New Imports:**
    
    ```solidity
    import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
    import "@openzeppelin/contracts/access/Ownable.sol";
    ```
    
    - **SafeERC20**: This is a library that provides safer methods for interacting with ERC20 tokens. It's like having a safety net when dealing with token transfers and approvals.
    - **Ownable**: This is a contract module that adds a basic access control mechanism to our contract. Think of it as adding a lock to your front door - only the owner has the key!
- **Contract Inheritance:**
    
    ```solidity
    contract CoreLoanPlatform is Ownable {
    ```
    
    - By adding `is Ownable`, our contract now inherits all the functionality of the Ownable contract. This is like our contract joining the "Ownable" family and getting all the family traits!
- **SafeERC20 Usage**:
    
    ```solidity
    using SafeERC20 for IERC20;
    ```
    
    - This tells our contract to use the safer methods from SafeERC20 whenever it's dealing with IERC20 tokens. It's like giving our contract a pair of safety goggles to wear whenever it handles tokens!
- **New Events:**
    - Events in Solidity are like announcements that your contract makes to the outside world. Imagine your contract is a sports commentator - events are the way it tells everyone what's happening in the game!
    
    ```solidity
    event LoanTaken(address indexed borrower, uint256 amount, uint256 collateral);
    event LoanRepaid(address indexed borrower, uint256 amount, uint256 interest);
    event CollateralDeposited(address indexed user, uint256 amount);
    event CollateralWithdrawn(address indexed user, uint256 amount);
    event BTCDeposited(address indexed lender, uint256 amount);
    event BTCWithdrawn(address indexed lender, uint256 amount);
    ```
    
    - Each of these events will be emitted (or announced) when something important happens in our contract. For example, when someone takes out a loan, we'll shout it out with `LoanTaken`!
- **New files:**
    - We have added two files `Bitcoin.sol` and `USD.sol` both of which are explained in the lesson moving ahead.
    - There is another import ERC20Permit in these files:
        
        ```solidity
        import "@openzeppelin/contracts/token/ERC20/extensions/ERC20Permit.sol";
        ```
        
    - This import adds the ERC20Permit functionality to our token. ERC20Permit is an extension of the ERC20 standard that allows for gasless token approvals. Here's why it's important:
        1. **Gasless Approvals**: It enables users to approve token spending without sending a transaction, saving on gas fees.
        2. **Enhanced UX**: Improves user experience by reducing the number of transactions needed for token interactions.
        3. **Signature-Based Permissions**: Uses cryptographic signatures to grant spending permissions, adding an extra layer of security.

Don’t worry if you see an error in your contract, it will be resolved once we add the constructor. Now, we'll dive into constructors and see how we can use these new tools in our contract's initialization


## Constructors: Launching Your Contract

A constructor is a special function in a Solidity contract that gets executed only once during the initial deployment of the contract. It's like the setup phase for your contract, where you can initialize important variables or perform essential actions.

**Key Points to Remember:**

- One constructor per contract guarantees a consistent starting point.
- Constructor code initializes the contract state (variables) during deployment.
- Public constructors allow anyone to deploy, while internal ones restrict deployment for internal use (like abstract classes).
- Deployed code only includes public functions and code reachable through them, not the constructor itself.
- A default empty constructor is provided if you don't define one.

![img](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C2%20assets%20-%20Start%20Building%20on%20Core/Core%20C2%20L10%20Image%201.webp?raw=true)

### Initializing state variables

The primary purpose of a constructor is to assign initial values to your contract’s state variables. These variables hold the data that defines the current state of your smart contract. By initializing them in the constructor, you ensure they have meaningful values from the get-go.

For example, in our lending dApp, we want to deposit USD as collateral to borrow BTC tokens. It means we need addresses at the beginning, where we can initialize the address from, where we have to fetch the USD, and an address where we have to transfer the BTC tokens. These addresses have to be initialized in the constructor.

Constructors can also be defined to accept arguments. This allows you to provide custom initial values during deployment. Just like we need for our Lending dApp.

Navigate to `build-lending-dapp-on-core/contracts/CoreLoanPlatform.sol`. We can write up the code as:

```solidity
constructor(address _USD, address _BTC) Ownable(msg.sender) {

	require(_USD != address(0) && _BTC != address(0), "Invalid token addresses");
	USD = IERC20(_USD);
	BTC = IERC20(_BTC);

}
```

This initializes the contract by setting the deployer as the owner using the `Ownable` constructor. It checks that the provided USD and BTC token addresses are not zero addresses, ensuring they are valid. If valid, it assigns these addresses to the `USD` and `BTC` state variables, casting them to the `IERC20` interface to allow interaction with the respective token contracts. This setup ensures the contract can manage USD and BTC tokens securely.

## Faucet Code Functions

We did discuss in our last lesson about our Tokens: USD and BTC. Here we will discuss how the faucet works where we will be using the mint function of our USD and BTC contracts which are the two mock currencies we will be using for testing. We have already added the files in our git but let’s discuss them here.

### USD.sol

We define a smart contract named "USD" that adheres to the ERC-20 standard and includes ownership and permit functionalities from OpenZeppelin's libraries. The contract inherits from `ERC20`, `Ownable`, and `ERC20Permit`. In the constructor, it initializes the token with a name and symbol, sets the contract deployer as the owner, and establishes the permit functionality. The contract includes a mint function, allowing new tokens to be minted to a specified address, with the constraint that the amount must not exceed 100 tokens (with 18 decimal places).

The code for this is as follows:

```solidity
// SPDX-License-Identifier: MIT
// Compatible with OpenZeppelin Contracts ^5.0.0
pragma solidity ^0.8.24;
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/ERC20Permit.sol";

contract USD is ERC20, Ownable, ERC20Permit {
	constructor(string memory name, string memory symbol)
			ERC20(name, symbol)
			Ownable(msg.sender)
			ERC20Permit(name)
	{}
	
	function mint(address to, uint256 amount) public {
		require(amount <= 100 * 10 ** 18, "amount must be less than 100");
		_mint(to, amount);
	}

}
```

### Bitcoin.sol

We define a smart contract named "Bitcoin" that follows the ERC-20 standard and includes ownership and permit functionalities from OpenZeppelin's libraries. The contract inherits from `ERC20`, `Ownable`, and `ERC20Permit`. The constructor initializes the token with a given name and symbol, sets the deployer as the owner, and sets up the permit functionality. The contract includes a `mint` function, which allows new tokens to be minted to a specified address, but restricts the minting amount to a maximum of 10 tokens (with 18 decimal places).

The code for this is as follows:

```solidity
// SPDX-License-Identifier: MIT
// Compatible with OpenZeppelin Contracts ^5.0.0
pragma solidity ^0.8.24;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/ERC20Permit.sol";

contract Bitcoin is ERC20, Ownable, ERC20Permit {
	constructor(string memory name, string memory symbol)
		ERC20(name, symbol)
		Ownable(msg.sender)
		ERC20Permit(name)
	{}
	
	function mint(address to, uint256 amount) public {
		require(amount <= 10 * 10 ** 18, "amount must be less than 10");
		_mint(to, amount);
	}

}
```

Now whenever you click on the faucet button in the frontend, the mint function of both the BTC and USD contracts are called to mint 10 and 100 tokens respectively to your wallet address

Okay, let’s learn about operators since we will use them in our code.

## Arithmetic Operators

Arithmetic operators are used to perform mathematical operations. Common arithmetic operators in Solidity include:

![img](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C2%20assets%20-%20Start%20Building%20on%20Core/Core%20C2%20L10%20Image%202.webp?raw=true)

## Relational Operators

Relational operators are used to compare values. Common relational operators in Solidity include:

![img](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C2%20assets%20-%20Start%20Building%20on%20Core/Core%20C2%20L10%20Image%203.webp?raw=true)

## Require Statement: Enforcing Conditions

The `require` statement is used to enforce certain conditions in your contract. If the condition evaluated by `require` is not met, the transaction is reverted, and any changes made to the state are undone. This is useful for input validation and ensuring that only valid transactions are processed.

Now, it’s time to put all our knowledge of constructors, operators, and `require` statements into practice and define the functionality of `depositCollateral`, `withdrawCollateral`, `borrowBTC`, and a few other functions.

### depositCollateral

The `depositCollateral` function allows users to deposit a specified amount of USD tokens as collateral. It first ensures the amount is greater than zero, then safely transfers the USD tokens from the user's address to the contract. The user's collateral balance is updated accordingly, and a `CollateralDeposited` event is emitted to log the transaction.

In Solidity, we can use **events** to log information on the blockchain, enabling smart contracts to communicate with external applications. When an event is emitted, it creates a log entry that external applications can listen to and react to. This is useful for notifications, debugging, and enabling interactions between contracts and dApps.

The code for this function is given below, make sure to add it to our code file of `CoreLoanPlatform.sol` under the `function depositCollateral(uint256 amount) external {` placeholder:

```solidity
function depositCollateral(uint256 amount) external {
	require(amount > 0, "Amount must be greater than 0");
	USD.safeTransferFrom(msg.sender, address(this), amount);
	userCollateral[msg.sender] += amount;
	emit CollateralDeposited(msg.sender, amount);
}
```

### withdrawCollateral

The `withdrawCollateral` function enables users to withdraw a specified amount of USD tokens from their collateral. It first checks that the amount is greater than zero and that the user has sufficient collateral. If the user has an active loan, it ensures that the remaining collateral after withdrawal is enough to cover the required collateral. Upon validation, the user's collateral balance is reduced, the USD tokens are safely transferred to the user, and a `CollateralWithdrawn` event is emitted to log the transaction.

The code for this function is given below, make sure to add it to our code file of `CoreLoanPlatform.sol` under the `function withdrawCollateral(uint256 amount) external {` placeholder:

```solidity
function withdrawCollateral(uint256 amount) external {
	require(amount > 0, "Amount must be greater than 0");
	require(userCollateral[msg.sender] >= amount, "Insufficient collateral");
	uint256 borrowedAmount = loans[msg.sender].active ? loans[msg.sender].amount : 0;
	uint256 requiredCollateral = (borrowedAmount * COLLATERAL_RATIO) / 100;
	
	require(userCollateral[msg.sender] - amount >= requiredCollateral, "Withdrawal would undercollateralize loan");
	userCollateral[msg.sender] -= amount;
	USD.safeTransfer(msg.sender, amount);
	emit CollateralWithdrawn(msg.sender, amount);
}
```

### borrowBTC

The `borrowBTC` function allows users to borrow a specified amount of BTC tokens. It ensures the requested amount is greater than zero and that the user has no existing active loans. It calculates the required collateral and checks if the user has sufficient collateral. It also ensures the borrowed amount is within the allowable limit based on the user's collateral and that the contract has enough BTC tokens. If all checks pass, it creates a new loan, transfers the BTC to the user, updates the total borrowed amount, and emits a `LoanTaken` event to log the transaction.

The code for this function is given below, make sure to add it to our code file of `CoreLoanPlatform.sol` under the `function borrowBTC(uint256 amount) external {` placeholder:

```solidity
function borrowBTC(uint256 amount) external {
	require(amount > 0, "Amount must be greater than 0");
	require(!loans[msg.sender].active, "Existing loan must be repaid first");
	
	uint256 requiredCollateral = (amount * COLLATERAL_RATIO) / 100;
	require(userCollateral[msg.sender] >= requiredCollateral, "Insufficient collateral");
	
	uint256 maxBorrowable = (userCollateral[msg.sender] * BORROWABLE_RATIO) / 100;
	require(amount <= maxBorrowable, "Borrow amount exceeds limit");
	require(BTC.balanceOf(address(this)) >= amount, "Insufficient BTC in contract");
	
	loans[msg.sender] = Loan(amount, requiredCollateral, block.timestamp, true);
	BTC.safeTransfer(msg.sender, amount);
	totalBorrowed = totalBorrowed + amount;
	emit LoanTaken(msg.sender, amount, requiredCollateral);
}
```

### Query Helper Functions

Finally, we’ll just add two more functions to our contract that will help us fetch the user’s collateral and borrowable amount.

```solidity
function getBorrowableAmount(address user) external view returns (uint256) {
	return (userCollateral[user] * BORROWABLE_RATIO) / 100;
}

function getUserCollateral(address user) external view returns (uint256) {
	return userCollateral[user];
}
```

This pretty much sums up the basics, let’s deploy and test everything out.

## Let’s try it out

First, let’s create a `.env` in the root directory and paste your private key against the `PRIVATE_KEY` variable.

![img](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C2%20assets%20-%20Start%20Building%20on%20Core/Core%20C2%20L10%20Image%204.webp?raw=true)

### Deployment script

Let’s update our deployment script according to our updated code. Here is how our script will look like now:

```solidity
const { buildModule } = require("@nomicfoundation/hardhat-ignition/modules");

module.exports = buildModule("BTCfi", (m) => {
	const BTC = m.contract("Bitcoin", ["Bitcoin", "BTC"]);
	const USD = m.contract("USD", ["US Dollar", "USD"]);
	const lending = m.contract("CoreLoanPlatform", [USD, BTC]);
	m.call(BTC, "transferOwnership", [lending]);
	m.call(USD, "transferOwnership", [lending]);
	
	return { BTC, USD, lending };
});
```

It defines the deployment of three contracts: `Bitcoin`, `USD`, and `CoreLoanPlatform`. And sets up a basic lending platform where `CoreLoanPlatform` can control the `Bitcoin` and `USD` tokens. Here's a brief explanation:

1. **Define Contracts**: It defines two contracts, `Bitcoin` (with the symbol "BTC") and `USD` (with the symbol "USD"). These contracts are given in the `contracts` folder and they are mock ERC20 tokens that will be dispensed via the faucet in our dApp
2. **Core Loan Platform**: It deploys a `CoreLoanPlatform` contract, initializing it with the `USD` and `BTC` contracts.
3. **Ownership Transfer**: The script then transfers ownership of the `Bitcoin` and `USD` contracts to the `CoreLoanPlatform` contract, allowing it to manage these tokens.
4. **Return Contracts**: Finally, it returns the deployed contracts for further use.

Now let’s install all the dependencies and compile our contract using the following:

```solidity
npm install
npx hardhat compile
```

Let’s deploy our contract using hardhat ignition. just like last time, make sure you delete the `./ignition/deployments` folder if it exists and run the following command in your terminal :

```solidity
npx hardhat ignition deploy ./ignition/modules/Deploy.js --network core_testnet
```

Copy the addresses of DAPP, USD, and BTC Contract addresses from the terminal and paste them against the corresponding variables in the `.env` file present inside the `*./interface/`* folder*.*

![img](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C2%20assets%20-%20Start%20Building%20on%20Core/Core%20C2%20L10%20Image%205.webp?raw=true)

Navigate to the react app and install the dependencies :

```solidity
cd interface
npm install
```

Copy the .json files containing the ABI from `*./artifacts/contracts*` to `*./interface/src/ABI/*` folder. You'll need to copy the following .json files from their respective folders

- Bitcoin.json
- CoreLoanPlatform.json
- IERC20.json
- USD.json

![img](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C2%20assets%20-%20Start%20Building%20on%20Core/Core%20C2%20L10%20Image%206.webp?raw=true)

Finally, let’s launch the frontend using:

```solidity
npm start
```

## Frontend Interaction

Let’s interact with our code. Follow the steps given below:

1. Connect your wallet by clicking on “Connect Wallet”.
![img](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C2%20assets%20-%20Start%20Building%20on%20Core/Core%20C2%20L10%20Image%207.webp?raw=true)

2. Next, fetch some tokens. Click on the faucet icon and accept the two prompts to receive tokens in your wallet.
![img](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C2%20assets%20-%20Start%20Building%20on%20Core/Core%20C2%20L10%20Image%208.webp?raw=true)

3. It’s time to stake.
    - Enter 10 or any value you want in the tab under “Stake or Withdraw Collateral”.
    - Click on the Stake button.
    - Click on Next, Approve, and then Confirm.
  ![img](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C2%20assets%20-%20Start%20Building%20on%20Core/Core%20C2%20L10%20Image%209.webp?raw=true)

4. After a few seconds, you can see the updated values:
![img](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C2%20assets%20-%20Start%20Building%20on%20Core/Core%20C2%20L10%20Image%2010.webp?raw=true)

5. And now it’s time to test our withdrawal functionality.
    - Enter 6 or any value you want in the tab under “Stake or Withdraw Collateral”
    - Click on the Withdraw button
    - Click on Confirm
![img](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C2%20assets%20-%20Start%20Building%20on%20Core/Core%20C2%20L10%20Image%2011.webp?raw=true)

6. After a few seconds, you can see the updated values:
![img](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C2%20assets%20-%20Start%20Building%20on%20Core/Core%20C2%20L10%20Image%2012.webp?raw=true)

7. Try BTC borrow too, but for now, it doesn’t work because we haven’t implemented lending functionality.
![img](https://github.com/0xmetaschool/Learning-Projects/blob/main/assests_for_all/Core%20C2%20assets%20-%20Start%20Building%20on%20Core/Core%20C2%20L10%20Image%2013.webp?raw=true)

And that’s it, our app now allows users to stake and withdraw whenever they wish. Onto the last step!

## Pushing It to Git

Make sure you are in `build-lending-dapp-on-core` folder.

And as always, remember to push your code to your GitHub repository using the following commands.

```solidity
git add .
git commit -m "code update"
git push origin HEAD:refs/heads/Branch2
```

## Wrap Up

We covered understanding constructors for initializing contracts, setting token addresses, and using arithmetic and relational operators along with the required statement. It included practical applications by implementing and explaining the deposit and withdrawal functions, followed by compiling and deploying the smart contract on the Core testnet.

In the next lesson, we will implement more functionalities. Stay tuned!
